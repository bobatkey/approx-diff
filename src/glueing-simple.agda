{-# OPTIONS --prop --postfix-projections --safe #-}

open import Level using (suc; _âŠ”_)
open import prop-setoid using (module â‰ˆ-Reasoning; IsEquivalence)
open import basics using (IsPreorder; IsMeet; IsTop; IsResidual; module â‰¤-Reasoning; IsJoin)
open import categories using (Category; HasProducts; HasExponentials; HasCoproducts; HasTerminal; IsTerminal)
open import functor using (Functor)
open import predicate-system using (PredicateSystem)

-- FIXME: refactor this into
--   1. glueing with predicates over ğ’ directly
--   2. pullback of PredicateSystems along product preserving functors

module glueing-simple {oâ‚ mâ‚ eâ‚ oâ‚‚ mâ‚‚ eâ‚‚}
  (ğ’ : Category oâ‚ mâ‚ eâ‚)
  (ğ’Ÿ : Category oâ‚‚ mâ‚‚ eâ‚‚) (ğ’ŸP : HasProducts ğ’Ÿ)
  (ğ’Ÿ-predicates : PredicateSystem ğ’Ÿ ğ’ŸP)
  (F : Functor ğ’ ğ’Ÿ) where

private
  module ğ’ = Category ğ’
  module ğ’Ÿ = Category ğ’Ÿ
  module ğ’ŸP = HasProducts ğ’ŸP
open Functor
open PredicateSystem ğ’Ÿ-predicates

âŠ‘-refl : âˆ€ {X}{P : Predicate X} â†’ P âŠ‘ P
âŠ‘-refl = âŠ‘-isPreorder .IsPreorder.refl

record Obj : Set (suc oâ‚ âŠ” suc mâ‚ âŠ” suc eâ‚ âŠ” suc oâ‚‚ âŠ” suc mâ‚‚ âŠ” suc eâ‚‚) where
  no-eta-equality
  field
    carrier : ğ’.obj
    pred    : Predicate (F .fobj carrier)
open Obj

record _=>_ (X Y : Obj) : Set (oâ‚ âŠ” mâ‚ âŠ” eâ‚ âŠ” oâ‚‚ âŠ” mâ‚‚ âŠ” eâ‚‚) where
  no-eta-equality
  field
    morph : X .carrier ğ’.â‡’ Y .carrier
    presv : X .pred âŠ‘ (Y .pred [ F .fmor morph ])
open _=>_

record _â‰ƒm_ {X Y} (f g : X => Y) : Prop (oâ‚ âŠ” mâ‚ âŠ” eâ‚ âŠ” oâ‚‚ âŠ” mâ‚‚ âŠ” eâ‚‚) where
  no-eta-equality
  field
    fâ‰ƒf : f .morph ğ’.â‰ˆ g .morph
open _â‰ƒm_

id : âˆ€ X â†’ X => X
id X .morph = ğ’.id _
id X .presv = begin
     X .pred                       â‰¤âŸ¨ []-id âŸ©
     X .pred [ ğ’Ÿ.id _ ]           â‰¤âŸ¨ []-cong (ğ’Ÿ.â‰ˆ-sym (F .fmor-id)) âŸ©
     X .pred [ F .fmor (ğ’.id _) ]
  âˆ
  where open â‰¤-Reasoning âŠ‘-isPreorder

_âˆ˜_ : âˆ€ {X Y Z} â†’ Y => Z â†’ X => Y â†’ X => Z
(f âˆ˜ g) .morph = f .morph ğ’.âˆ˜ g .morph
_âˆ˜_ {X}{Y}{Z} f g .presv = begin
    X .pred                                                 â‰¤âŸ¨ g .presv âŸ©
    Y .pred [ F .fmor (g .morph) ]                          â‰¤âŸ¨ (f .presv) [ F .fmor (g .morph) ]m âŸ©
    (Z .pred [ F .fmor (f .morph) ]) [ F .fmor (g .morph) ] â‰¤âŸ¨ []-comp _ _ âŸ©
    Z .pred [ F .fmor (f .morph) ğ’Ÿ.âˆ˜ F .fmor (g .morph) ]  â‰¤âŸ¨ []-cong (ğ’Ÿ.â‰ˆ-sym (F .fmor-comp _ _)) âŸ©
    Z .pred [ F .fmor (f .morph ğ’.âˆ˜ g .morph) ]
  âˆ
  where open â‰¤-Reasoning âŠ‘-isPreorder

cat : Category (suc oâ‚ âŠ” suc mâ‚ âŠ” suc eâ‚ âŠ” suc oâ‚‚ âŠ” suc mâ‚‚ âŠ” suc eâ‚‚) (oâ‚ âŠ” mâ‚ âŠ” eâ‚ âŠ” oâ‚‚ âŠ” mâ‚‚ âŠ” eâ‚‚) (oâ‚ âŠ” mâ‚ âŠ” eâ‚ âŠ” oâ‚‚ âŠ” mâ‚‚ âŠ” eâ‚‚)
cat .Category.obj = Obj
cat .Category._â‡’_ = _=>_
cat .Category._â‰ˆ_ = _â‰ƒm_
cat .Category.isEquiv .IsEquivalence.refl .fâ‰ƒf = ğ’.â‰ˆ-refl
cat .Category.isEquiv .IsEquivalence.sym e .fâ‰ƒf = ğ’.â‰ˆ-sym (e .fâ‰ƒf)
cat .Category.isEquiv .IsEquivalence.trans eâ‚ eâ‚‚ .fâ‰ƒf = ğ’.â‰ˆ-trans (eâ‚ .fâ‰ƒf) (eâ‚‚ .fâ‰ƒf)
cat .Category.id = id
cat .Category._âˆ˜_ = _âˆ˜_
cat .Category.âˆ˜-cong eâ‚ eâ‚‚ .fâ‰ƒf = ğ’.âˆ˜-cong (eâ‚ .fâ‰ƒf) (eâ‚‚ .fâ‰ƒf)
cat .Category.id-left .fâ‰ƒf = ğ’.id-left
cat .Category.id-right .fâ‰ƒf = ğ’.id-right
cat .Category.assoc f g h .fâ‰ƒf = ğ’.assoc (f .morph) (g .morph) (h .morph)

-- Binary Coproducts
module coproducts (CP : HasCoproducts ğ’) where

  private
    module CP = HasCoproducts CP

  _[+]_ : Obj â†’ Obj â†’ Obj
  (X [+] Y) .carrier = CP.coprod (X .carrier) (Y .carrier)
  (X [+] Y) .pred = (X .pred âŸ¨ F .fmor CP.inâ‚ âŸ©) ++ (Y .pred âŸ¨ F .fmor CP.inâ‚‚ âŸ©)

  inâ‚ : âˆ€ {X Y} â†’ X => (X [+] Y)
  inâ‚ .morph = CP.inâ‚
  inâ‚ {X} {Y} .presv = begin
      X .pred
    â‰¤âŸ¨ unit _ âŸ©
      X .pred âŸ¨ F .fmor CP.inâ‚ âŸ© [ F .fmor CP.inâ‚ ]
    â‰¤âŸ¨ ++-isJoin .IsJoin.inl [ _ ]m âŸ©
      ((X .pred âŸ¨ F .fmor CP.inâ‚ âŸ©) ++ (Y .pred âŸ¨ F .fmor CP.inâ‚‚ âŸ©)) [ F .fmor CP.inâ‚ ]
    âˆ
    where open â‰¤-Reasoning âŠ‘-isPreorder

  inâ‚‚ : âˆ€ {X Y} â†’ Y => (X [+] Y)
  inâ‚‚ .morph = CP.inâ‚‚
  inâ‚‚ {X} {Y} .presv = begin
      Y .pred
    â‰¤âŸ¨ unit _ âŸ©
      Y .pred âŸ¨ F .fmor CP.inâ‚‚ âŸ© [ F .fmor CP.inâ‚‚ ]
    â‰¤âŸ¨ ++-isJoin .IsJoin.inr [ _ ]m âŸ©
      ((X .pred âŸ¨ F .fmor CP.inâ‚ âŸ©) ++ (Y .pred âŸ¨ F .fmor CP.inâ‚‚ âŸ©)) [ F .fmor CP.inâ‚‚ ]
    âˆ
    where open â‰¤-Reasoning âŠ‘-isPreorder

  copair : âˆ€ {X Y Z} â†’ X => Z â†’ Y => Z â†’ (X [+] Y) => Z
  copair f g .morph = CP.copair (f .morph) (g .morph)
  copair {X} {Y} {Z} f g .presv = begin
      (X .pred âŸ¨ F .fmor CP.inâ‚ âŸ©) ++ (Y .pred âŸ¨ F .fmor CP.inâ‚‚ âŸ©)
    â‰¤âŸ¨ IsJoin.mono ++-isJoin (f .presv âŸ¨ _ âŸ©m) (g .presv âŸ¨ _ âŸ©m) âŸ©
      (Z .pred [ F .fmor (f .morph) ] âŸ¨ F .fmor CP.inâ‚ âŸ©) ++ (Z .pred [ F .fmor (g .morph) ] âŸ¨ F .fmor CP.inâ‚‚ âŸ©)
    â‰¤âŸ¨ IsJoin.mono ++-isJoin ([]-cong (F .fmor-cong (ğ’.â‰ˆ-sym (CP.copair-inâ‚ _ _))) âŸ¨ _ âŸ©m)
                             ([]-cong (F .fmor-cong (ğ’.â‰ˆ-sym (CP.copair-inâ‚‚ _ _))) âŸ¨ _ âŸ©m) âŸ©
      (Z .pred [ F .fmor (CP.copair (f .morph) (g .morph) ğ’.âˆ˜ CP.inâ‚) ] âŸ¨ F .fmor CP.inâ‚ âŸ©)
        ++
      (Z .pred [ F .fmor (CP.copair (f .morph) (g .morph) ğ’.âˆ˜ CP.inâ‚‚) ] âŸ¨ F .fmor CP.inâ‚‚ âŸ©)
    â‰¤âŸ¨ IsJoin.mono ++-isJoin ([]-cong (F .fmor-comp _ _) âŸ¨ _ âŸ©m)
                             ([]-cong (F .fmor-comp _ _) âŸ¨ _ âŸ©m) âŸ©
      (Z .pred [ F .fmor (CP.copair (f .morph) (g .morph)) ğ’Ÿ.âˆ˜ F .fmor CP.inâ‚ ] âŸ¨ F .fmor CP.inâ‚ âŸ©)
        ++
      (Z .pred [ F .fmor (CP.copair (f .morph) (g .morph)) ğ’Ÿ.âˆ˜ F .fmor CP.inâ‚‚ ] âŸ¨ F .fmor CP.inâ‚‚ âŸ©)
    â‰¤âŸ¨ IsJoin.mono ++-isJoin (([]-compâ»Â¹ _ _) âŸ¨ _ âŸ©m) (([]-compâ»Â¹ _ _) âŸ¨ _ âŸ©m) âŸ©
      (Z .pred [ F .fmor (CP.copair (f .morph) (g .morph)) ] [ F .fmor CP.inâ‚ ] âŸ¨ F .fmor CP.inâ‚ âŸ©)
        ++
      (Z .pred [ F .fmor (CP.copair (f .morph) (g .morph)) ] [ F .fmor CP.inâ‚‚ ] âŸ¨ F .fmor CP.inâ‚‚ âŸ©)
    â‰¤âŸ¨ IsJoin.[_,_] ++-isJoin (counit _) (counit _) âŸ©
      Z .pred [ F .fmor (CP.copair (f .morph) (g .morph)) ]
    âˆ
    where open â‰¤-Reasoning âŠ‘-isPreorder

  coproducts : HasCoproducts cat
  coproducts .HasCoproducts.coprod = _[+]_
  coproducts .HasCoproducts.inâ‚ = inâ‚
  coproducts .HasCoproducts.inâ‚‚ = inâ‚‚
  coproducts .HasCoproducts.copair = copair
  coproducts .HasCoproducts.copair-cong eâ‚ eâ‚‚ .fâ‰ƒf = CP.copair-cong (eâ‚ .fâ‰ƒf) (eâ‚‚ .fâ‰ƒf)
  coproducts .HasCoproducts.copair-inâ‚ f g .fâ‰ƒf = CP.copair-inâ‚ (f .morph) (g .morph)
  coproducts .HasCoproducts.copair-inâ‚‚ f g .fâ‰ƒf = CP.copair-inâ‚‚ (f .morph) (g .morph)
  coproducts .HasCoproducts.copair-ext f .fâ‰ƒf = CP.copair-ext (f .morph)

-- products and exponentials
module products-and-exponentials
         (T : HasTerminal ğ’) (P : HasProducts ğ’) (E : HasExponentials ğ’ P)
         (mul   : âˆ€ {x y} â†’ ğ’ŸP.prod (F .fobj x) (F .fobj y) ğ’Ÿ.â‡’ F .fobj (P .HasProducts.prod x y))
         (mulâ»Â¹ : âˆ€ {x y} â†’ F .fobj (P .HasProducts.prod x y) ğ’Ÿ.â‡’ ğ’ŸP.prod (F .fobj x) (F .fobj y))
         (mul-inv : âˆ€ {x y} â†’ (mul {x} {y} ğ’Ÿ.âˆ˜ mulâ»Â¹) ğ’Ÿ.â‰ˆ ğ’Ÿ.id _)
         (mul-natural : âˆ€ {x x' y y'} {f : x ğ’.â‡’ x'} {g : y ğ’.â‡’ y'} â†’ (F .fmor (HasProducts.prod-m P f g) ğ’Ÿ.âˆ˜ mul) ğ’Ÿ.â‰ˆ (mul ğ’Ÿ.âˆ˜ ğ’ŸP.prod-m (F .fmor f) (F .fmor g)))
         (F-pâ‚   : âˆ€ {x y} â†’ (F .fmor (P .HasProducts.pâ‚ {x} {y}) ğ’Ÿ.âˆ˜ mul) ğ’Ÿ.â‰ˆ ğ’ŸP.pâ‚)
     where

  private
    module T = HasTerminal T
    module P = HasProducts P
    module E = HasExponentials E

  F-pâ‚' : âˆ€ {x y} â†’ F .fmor (P .HasProducts.pâ‚ {x} {y}) ğ’Ÿ.â‰ˆ (ğ’ŸP.pâ‚ ğ’Ÿ.âˆ˜ mulâ»Â¹)
  F-pâ‚' {x} {y} = begin
      F .fmor (P .HasProducts.pâ‚ {x} {y})                       â‰ˆË˜âŸ¨ ğ’Ÿ.id-right âŸ©
      F .fmor (P .HasProducts.pâ‚ {x} {y}) ğ’Ÿ.âˆ˜ ğ’Ÿ.id _           â‰ˆË˜âŸ¨ ğ’Ÿ.âˆ˜-cong ğ’Ÿ.â‰ˆ-refl mul-inv âŸ©
      F .fmor (P .HasProducts.pâ‚ {x} {y}) ğ’Ÿ.âˆ˜ (mul ğ’Ÿ.âˆ˜ mulâ»Â¹)  â‰ˆË˜âŸ¨ ğ’Ÿ.assoc _ _ _ âŸ©
      (F .fmor (P .HasProducts.pâ‚ {x} {y}) ğ’Ÿ.âˆ˜ mul) ğ’Ÿ.âˆ˜ mulâ»Â¹  â‰ˆâŸ¨ ğ’Ÿ.âˆ˜-cong F-pâ‚ ğ’Ÿ.â‰ˆ-refl âŸ©
      ğ’ŸP.pâ‚ ğ’Ÿ.âˆ˜ mulâ»Â¹
    âˆ where open â‰ˆ-Reasoning ğ’Ÿ.isEquiv

  open IsMeet

  -- Terminal
  [âŠ¤] : Obj
  [âŠ¤] .carrier = T.witness
  [âŠ¤] .pred = TT

  to-terminal : âˆ€ {X} â†’ X => [âŠ¤]
  to-terminal .morph = T.is-terminal .IsTerminal.to-terminal
  to-terminal {X} .presv = begin
      X .pred
    â‰¤âŸ¨ TT-isTop .IsTop.â‰¤-top âŸ©
      TT
    â‰¤âŸ¨ []-TT âŸ©
      TT [ F .fmor (T.is-terminal .IsTerminal.to-terminal) ]
    âˆ
    where open â‰¤-Reasoning âŠ‘-isPreorder

  terminal : HasTerminal cat
  terminal .HasTerminal.witness = [âŠ¤]
  terminal .HasTerminal.is-terminal .IsTerminal.to-terminal = to-terminal
  terminal .HasTerminal.is-terminal .IsTerminal.to-terminal-ext f .fâ‰ƒf =
    T.is-terminal .IsTerminal.to-terminal-ext (f .morph)

  -- Products
  _[Ã—]_ : Obj â†’ Obj â†’ Obj
  (X [Ã—] Y) .carrier = P.prod (X .carrier) (Y .carrier)
  (X [Ã—] Y) .pred = (X .pred [ F .fmor P.pâ‚ ]) && (Y .pred [ F .fmor P.pâ‚‚ ])

  pâ‚ : âˆ€ {X Y} â†’ (X [Ã—] Y) => X
  pâ‚ .morph = P.pâ‚
  pâ‚ .presv = &&-isMeet .Ï€â‚

  pâ‚‚ : âˆ€ {X Y} â†’ (X [Ã—] Y) => Y
  pâ‚‚ .morph = P.pâ‚‚
  pâ‚‚ .presv = &&-isMeet .Ï€â‚‚

  pair : âˆ€ {X Y Z} â†’ X => Y â†’ X => Z â†’ X => (Y [Ã—] Z)
  pair f g .morph = P.pair (f .morph) (g .morph)
  pair {X} {Y} {Z} f g .presv = begin
      X .pred
    â‰¤âŸ¨ &&-isMeet .âŸ¨_,_âŸ© (f .presv) (g .presv) âŸ©
      (Y .pred [ F .fmor (f .morph) ]) && (Z .pred [ F .fmor (g .morph) ])
    â‰¤âŸ¨ mono &&-isMeet ([]-cong (F .fmor-cong (ğ’.â‰ˆ-sym (P.pair-pâ‚ _ _)))) ([]-cong (F .fmor-cong (ğ’.â‰ˆ-sym (P.pair-pâ‚‚ _ _)))) âŸ©
      (Y .pred [ F .fmor (P.pâ‚ ğ’.âˆ˜ P.pair (f .morph) (g .morph)) ]) && (Z .pred [ F .fmor (P.pâ‚‚ ğ’.âˆ˜ P.pair (f .morph) (g .morph)) ])
    â‰¤âŸ¨ mono &&-isMeet ([]-cong (F .fmor-comp _ _)) ([]-cong (F .fmor-comp _ _)) âŸ©
      (Y .pred [ F .fmor P.pâ‚ ğ’Ÿ.âˆ˜ F .fmor (P.pair (f .morph) (g .morph)) ]) && (Z .pred [ F .fmor P.pâ‚‚ ğ’Ÿ.âˆ˜ F .fmor (P.pair (f .morph) (g .morph)) ])
    â‰¤âŸ¨ mono &&-isMeet ([]-compâ»Â¹ _ _) ([]-compâ»Â¹ _ _) âŸ©
      ((Y .pred [ F .fmor P.pâ‚ ]) [ F .fmor (P.pair (f .morph) (g .morph)) ]) && ((Z .pred [ F .fmor P.pâ‚‚ ]) [ F .fmor (P.pair (f .morph) (g .morph)) ])
    â‰¤âŸ¨ []-&& âŸ©
      ((Y .pred [ F .fmor P.pâ‚ ]) && (Z .pred [ F .fmor P.pâ‚‚ ])) [ F .fmor (P.pair (f .morph) (g .morph)) ]
    âˆ where open â‰¤-Reasoning âŠ‘-isPreorder

  products : HasProducts cat
  products .HasProducts.prod = _[Ã—]_
  products .HasProducts.pâ‚ = pâ‚
  products .HasProducts.pâ‚‚ = pâ‚‚
  products .HasProducts.pair = pair
  products .HasProducts.pair-cong eâ‚ eâ‚‚ .fâ‰ƒf = P.pair-cong (eâ‚ .fâ‰ƒf) (eâ‚‚ .fâ‰ƒf)
  products .HasProducts.pair-pâ‚ f g .fâ‰ƒf = P.pair-pâ‚ (f .morph) (g .morph)
  products .HasProducts.pair-pâ‚‚ f g .fâ‰ƒf = P.pair-pâ‚‚ (f .morph) (g .morph)
  products .HasProducts.pair-ext f .fâ‰ƒf = P.pair-ext (f .morph)

  -- Exponentials
  _[â†’]_ : Obj â†’ Obj â†’ Obj
  (X [â†’] Y) .carrier = E.exp (X .carrier) (Y .carrier)
  (X [â†’] Y) .pred = â‹€ (((X .pred [ F .fmor P.pâ‚‚ ]) ==> (Y .pred [ F .fmor E.eval ])) [ mul ])

  eval : âˆ€ {X Y} â†’ ((X [â†’] Y) [Ã—] X) => Y
  eval .morph = E.eval
  eval {X} {Y} .presv = begin
      (â‹€ (((X .pred [ F .fmor P.pâ‚‚ ]) ==> (Y .pred [ F .fmor E.eval ])) [ mul ]) [ F .fmor P.pâ‚ ]) && (X .pred [ F .fmor P.pâ‚‚ ])
    â‰¤âŸ¨ mono &&-isMeet ([]-cong F-pâ‚') âŠ‘-refl âŸ©
      (â‹€ (((X .pred [ F .fmor P.pâ‚‚ ]) ==> (Y .pred [ F .fmor E.eval ])) [ mul ]) [ ğ’ŸP.pâ‚ ğ’Ÿ.âˆ˜ mulâ»Â¹ ]) && (X .pred [ F .fmor P.pâ‚‚ ])
    â‰¤âŸ¨ mono &&-isMeet ([]-compâ»Â¹ _ _) âŠ‘-refl âŸ©
      ((â‹€ (((X .pred [ F .fmor P.pâ‚‚ ]) ==> (Y .pred [ F .fmor E.eval ])) [ mul ]) [ ğ’ŸP.pâ‚ ]) [ mulâ»Â¹ ]) && (X .pred [ F .fmor P.pâ‚‚ ])
    â‰¤âŸ¨ mono &&-isMeet (â‹€-eval [ _ ]m) âŠ‘-refl âŸ©
      ((((X .pred [ F .fmor P.pâ‚‚ ]) ==> (Y .pred [ F .fmor E.eval ])) [ mul ]) [ mulâ»Â¹ ]) && (X .pred [ F .fmor P.pâ‚‚ ])
    â‰¤âŸ¨ mono &&-isMeet ([]-comp _ _) âŠ‘-refl âŸ©
      (((X .pred [ F .fmor P.pâ‚‚ ]) ==> (Y .pred [ F .fmor E.eval ])) [ mul ğ’Ÿ.âˆ˜ mulâ»Â¹ ]) && (X .pred [ F .fmor P.pâ‚‚ ])
    â‰¤âŸ¨ mono &&-isMeet ([]-cong mul-inv) âŠ‘-refl âŸ©
      (((X .pred [ F .fmor P.pâ‚‚ ]) ==> (Y .pred [ F .fmor E.eval ])) [ ğ’Ÿ.id _ ]) && (X .pred [ F .fmor P.pâ‚‚ ])
    â‰¤âŸ¨ mono &&-isMeet []-idâ»Â¹ âŠ‘-refl âŸ©
      ((X .pred [ F .fmor P.pâ‚‚ ]) ==> (Y .pred [ F .fmor E.eval ])) && (X .pred [ F .fmor P.pâ‚‚ ])
    â‰¤âŸ¨ ==>-residual .IsResidual.eval âŸ©
      Y .pred [ F .fmor E.eval ]
    âˆ
    where open â‰¤-Reasoning âŠ‘-isPreorder

  lambda : âˆ€ {X Y Z} â†’ (X [Ã—] Y) => Z â†’ X => (Y [â†’] Z)
  lambda f .morph = E.lambda (f .morph)
  lambda {X} {Y} {Z} f .presv = begin
      X .pred
    â‰¤âŸ¨ â‹€-lambda lemma âŸ©
      â‹€ ((((Y .pred [ F .fmor P.pâ‚‚ ]) ==> (Z .pred [ F .fmor E.eval ])) [ mul ]) [ ğ’ŸP.prod-m (F .fmor (E.lambda (f .morph))) (ğ’Ÿ.id _) ])
    â‰¤âŸ¨ â‹€-[] âŸ©
      â‹€ (((Y .pred [ F .fmor P.pâ‚‚ ]) ==> (Z .pred [ F .fmor E.eval ])) [ mul ]) [ F .fmor (E.lambda (f .morph)) ]
    âˆ
    where
      lemma : (X .pred [ ğ’ŸP.pâ‚ ]) âŠ‘ ((((Y .pred [ F .fmor P.pâ‚‚ ]) ==> (Z .pred [ F .fmor E.eval ])) [ mul ]) [ ğ’ŸP.prod-m (F .fmor (E.lambda (f .morph))) (ğ’Ÿ.id _) ])
      lemma = begin
          X .pred [ ğ’ŸP.pâ‚ ]
        â‰¤âŸ¨ []-cong (ğ’Ÿ.â‰ˆ-sym F-pâ‚) âŸ©
          X .pred [ F .fmor P.pâ‚ ğ’Ÿ.âˆ˜ mul ]
        â‰¤âŸ¨ []-compâ»Â¹ _ _ âŸ©
          (X .pred [ F .fmor P.pâ‚ ]) [ mul ]
        â‰¤âŸ¨ (==>-residual .IsResidual.lambda (f .presv)) [ _ ]m âŸ©
          ((Y .pred [ F .fmor P.pâ‚‚ ]) ==> (Z .pred [ F .fmor (f .morph) ])) [ mul ]
        â‰¤âŸ¨ IsResidual.-âˆ™-mono ==>-residual ([]-cong (F .fmor-cong ğ’.id-left)) âŠ‘-refl [ mul ]m âŸ©
          ((Y .pred [ F .fmor (ğ’.id _ ğ’.âˆ˜ P.pâ‚‚) ]) ==> (Z .pred [ F .fmor (f .morph) ])) [ mul ]
        â‰¤âŸ¨ IsResidual.-âˆ™-mono ==>-residual ([]-cong (F .fmor-cong (P.pair-pâ‚‚ _ _))) ([]-cong (F .fmor-cong (ğ’.â‰ˆ-sym (E.eval-lambda _)))) [ mul ]m âŸ©
          ((Y .pred [ F .fmor (P.pâ‚‚ ğ’.âˆ˜ P.prod-m (E.lambda (f .morph)) (ğ’.id _)) ]) ==> (Z .pred [ F .fmor (E.eval ğ’.âˆ˜ P.prod-m (E.lambda (f .morph)) (ğ’.id _)) ])) [ mul ]
        â‰¤âŸ¨ IsResidual.-âˆ™-mono ==>-residual ([]-cong (ğ’Ÿ.â‰ˆ-sym (F .fmor-comp _ _))) ([]-cong (F .fmor-comp _ _)) [ mul ]m âŸ©
          ((Y .pred [ F .fmor P.pâ‚‚ ğ’Ÿ.âˆ˜ F .fmor (P.prod-m (E.lambda (f .morph)) (ğ’.id _)) ]) ==> (Z .pred [ F .fmor E.eval ğ’Ÿ.âˆ˜ F .fmor (P.prod-m (E.lambda (f .morph)) (ğ’.id _)) ])) [ mul ]
        â‰¤âŸ¨ IsResidual.-âˆ™-mono ==>-residual ([]-comp _ _) ([]-compâ»Â¹ _ _) [ mul ]m âŸ©
          (((Y .pred [ F .fmor P.pâ‚‚ ])  [ F .fmor (P.prod-m (E.lambda (f .morph)) (ğ’.id _)) ]) ==> ((Z .pred [ F .fmor E.eval ]) [ F .fmor (P.prod-m (E.lambda (f .morph)) (ğ’.id _)) ])) [ mul ]
        â‰¤âŸ¨ []-==> [ mul ]m âŸ©
          (((Y .pred [ F .fmor P.pâ‚‚ ]) ==> (Z .pred [ F .fmor E.eval ])) [ F .fmor (P.prod-m (E.lambda (f .morph)) (ğ’.id _)) ]) [ mul ]
        â‰¤âŸ¨ []-comp _ _ âŸ©
          ((Y .pred [ F .fmor P.pâ‚‚ ]) ==> (Z .pred [ F .fmor E.eval ])) [ F .fmor (P.prod-m (E.lambda (f .morph)) (ğ’.id _)) ğ’Ÿ.âˆ˜ mul ]
        â‰¤âŸ¨ []-cong mul-natural âŸ©
          ((Y .pred [ F .fmor P.pâ‚‚ ]) ==> (Z .pred [ F .fmor E.eval ])) [ mul ğ’Ÿ.âˆ˜ ğ’ŸP.prod-m (F .fmor (E.lambda (f .morph))) (F .fmor (ğ’.id _)) ]
        â‰¤âŸ¨ []-cong (ğ’Ÿ.âˆ˜-cong ğ’Ÿ.â‰ˆ-refl (ğ’ŸP.prod-m-cong ğ’Ÿ.â‰ˆ-refl (F .fmor-id))) âŸ©
          ((Y .pred [ F .fmor P.pâ‚‚ ]) ==> (Z .pred [ F .fmor E.eval ])) [ mul ğ’Ÿ.âˆ˜ ğ’ŸP.prod-m (F .fmor (E.lambda (f .morph))) (ğ’Ÿ.id _) ]
        â‰¤âŸ¨ []-compâ»Â¹ _ _ âŸ©
          (((Y .pred [ F .fmor P.pâ‚‚ ]) ==> (Z .pred [ F .fmor E.eval ])) [ mul ]) [ ğ’ŸP.prod-m (F .fmor (E.lambda (f .morph))) (ğ’Ÿ.id _) ]
        âˆ
        where open â‰¤-Reasoning âŠ‘-isPreorder
      open â‰¤-Reasoning âŠ‘-isPreorder

  exponentials : HasExponentials cat products
  exponentials .HasExponentials.exp = _[â†’]_
  exponentials .HasExponentials.eval = eval
  exponentials .HasExponentials.lambda = lambda
  exponentials .HasExponentials.lambda-cong e .fâ‰ƒf = E.lambda-cong (e .fâ‰ƒf)
  exponentials .HasExponentials.eval-lambda f .fâ‰ƒf = E.eval-lambda (f .morph)
  exponentials .HasExponentials.lambda-ext f .fâ‰ƒf = E.lambda-ext (f .morph)
