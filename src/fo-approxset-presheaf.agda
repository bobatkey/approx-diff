{-# OPTIONS --postfix-projections --without-K #-}

module fo-approxset-presheaf where

open import Level
open import Data.Product using (_Ã—_; _,_; projâ‚; projâ‚‚)
open import Data.Sum using (injâ‚; injâ‚‚)
open import Data.Unit using (tt)
open import Function renaming (id to idâ‚›; _âˆ˜_ to _âˆ˜â‚›_)
open import Relation.Binary using (Setoid; IsEquivalence)
open import Relation.Binary.PropositionalEquality
  using (cong; congâ‚‚; _â‰¡_; setoid) renaming (refl to â‰¡-refl; trans to â‰¡-trans)
open IsEquivalence
open Setoid using (Carrier; _â‰ˆ_; isEquivalence)
open import basics
open import fo-approxset
    using (FOApproxSet; â„’-map; â„’-map-resp-â‰ƒ; â„’-map-preserves-id; â„’-map-preserves-âˆ˜; â„’-unit-commute)
    renaming (
      _â‡’_ to _â‡’â‚_; _â‰ƒm_ to _â‰ƒmâ‚_; â‰ƒm-setoid to â‰ƒmâ‚-setoid; id to idâ‚; _âˆ˜_ to _âˆ˜â‚_; _âŠ—_ to _âŠ—â‚_;
      âˆ˜-resp-â‰ƒm to âˆ˜â‚-resp-â‰ƒmâ‚; âˆ˜-assoc to âˆ˜â‚-assoc; âˆ˜-unitâ‚— to âˆ˜â‚-unitâ‚—; âˆ˜-unitáµ£ to âˆ˜â‚-unitáµ£;
      â„’ to â„’â‚; â„’-unit to â„’â‚-unit; â„’-join to â„’â‚-join
    )

-- Presheaf on FOApproxSet.
record FOApproxSetPSh a : Set (suc a) where
  field
    obj : FOApproxSet â†’ Setoid a a
    map : âˆ€ {X Y} â†’ (X â‡’â‚ Y) â†’ obj Y .Carrier â†’ obj X .Carrier
    map-resp-â‰ˆ : âˆ€ {X Y} {f f' : X â‡’â‚ Y} â†’ f â‰ƒmâ‚ f' â†’ âˆ€ {x y} â†’ obj Y ._â‰ˆ_ x y â†’ obj X ._â‰ˆ_ (map f x) (map f' y)
    preserves-âˆ˜ : âˆ€ {X Y Z} (f : Y â‡’â‚ Z) (g : X â‡’â‚ Y) â†’ âˆ€ x â†’ obj X ._â‰ˆ_ (map g (map f x)) (map (f âˆ˜â‚ g) x)
    preserves-id : âˆ€ {X Y} (f : X â‡’â‚ Y) â†’ âˆ€ x â†’ obj X ._â‰ˆ_ (idâ‚› (map f x)) (map f (idâ‚› x))

open FOApproxSetPSh

record _â‡’_ {a b} (F : FOApproxSetPSh a) (G : FOApproxSetPSh b) : Set (suc (a âŠ” b)) where
  field
    at : âˆ€ X â†’ F .obj X .Carrier â†’ G .obj X .Carrier
    at-resp-â‰ˆ : âˆ€ X {x y} â†’ F .obj X ._â‰ˆ_ x y â†’ G .obj X ._â‰ˆ_ (at X x) (at X y)
    commute : âˆ€ {X Y} (f : X â‡’â‚ Y) â†’ âˆ€ x â†’ G .obj X ._â‰ˆ_ (at X (F .map f x)) (G .map f (at Y x))

open _â‡’_

record _â‰ƒm_ {a b} {F : FOApproxSetPSh a} {G : FOApproxSetPSh b} (Î· Î¶ : F â‡’ G) : Set (suc (a âŠ” b)) where
  field
    eqat : âˆ€ X {x x'} â†’ F .obj X ._â‰ˆ_ x x' â†’ G .obj X ._â‰ˆ_ (Î· .at X x) (Î¶ .at X x')

open _â‰ƒm_

module _ where
  â‰ƒm-setoid : âˆ€ {a b} (F : FOApproxSetPSh a) (G : FOApproxSetPSh b) â†’ Setoid (suc (a âŠ” b)) (suc (a âŠ” b))
  â‰ƒm-setoid F G .Carrier = F â‡’ G
  â‰ƒm-setoid F G ._â‰ˆ_ Î· Î¶ = Î· â‰ƒm Î¶
  â‰ƒm-setoid F G .isEquivalence .refl {Î·} .eqat X x = Î· .at-resp-â‰ˆ X x
  â‰ƒm-setoid F G .isEquivalence .sym Î·â‰ƒÎ¶ .eqat X x =
    G .obj X .isEquivalence .sym (Î·â‰ƒÎ¶ .eqat X (F .obj X .isEquivalence .sym x))
  â‰ƒm-setoid F G .isEquivalence .trans Î·â‰ƒÎ¶ Î¶â‰ƒÎµ .eqat X x =
    G .obj X .isEquivalence .trans (Î·â‰ƒÎ¶ .eqat X x) (Î¶â‰ƒÎµ .eqat X (F .obj X .isEquivalence .refl))

-- Definitions for category
id : âˆ€ {a} {F : FOApproxSetPSh a} â†’ F â‡’ F
id .at X = idâ‚›
id .at-resp-â‰ˆ X = idâ‚›
id {F = F} .commute = F .preserves-id

_âˆ˜_ : âˆ€ {a} {F G H : FOApproxSetPSh a} â†’ G â‡’ H â†’ F â‡’ G â†’ F â‡’ H
(Î¶ âˆ˜ Î·) .at X = Î¶ .at X âˆ˜â‚› Î· .at X
(Î¶ âˆ˜ Î·) .at-resp-â‰ˆ X = Î¶ .at-resp-â‰ˆ X âˆ˜â‚› Î· .at-resp-â‰ˆ X
_âˆ˜_ {H = H} Î¶ Î· .commute {X}{Y} f y =
  H .obj X .isEquivalence .trans (Î¶ .at-resp-â‰ˆ X (Î· .commute f y)) (Î¶ .commute f (Î· .at Y y))

infixr 10 _âˆ˜_

-- Terminal object
module _ where
  open import Data.Unit.Properties renaming (â‰¡-setoid to ğŸ™) public

  âŠ¤ : FOApproxSetPSh 0â„“
  âŠ¤ .obj X = ğŸ™
  âŠ¤ .map f _ = tt
  âŠ¤ .map-resp-â‰ˆ f _ = ğŸ™ .isEquivalence .refl
  âŠ¤ .preserves-âˆ˜ f g _ = ğŸ™ .isEquivalence .refl
  âŠ¤ .preserves-id f _ = ğŸ™ .isEquivalence .refl

  terminal : âˆ€ {a} {F : FOApproxSetPSh a} â†’ F â‡’ âŠ¤
  terminal .at X _ = tt
  terminal .at-resp-â‰ˆ X _ = ğŸ™ .isEquivalence .refl
  terminal .commute f x = ğŸ™ .isEquivalence .refl

-- Products
_âŠ—_ : âˆ€ {a b} â†’ FOApproxSetPSh a â†’ FOApproxSetPSh b â†’ FOApproxSetPSh (a âŠ” b)
(F âŠ— G) .obj X = âŠ—-setoid (F .obj X) (G .obj X)
(F âŠ— G) .map f (x , y) .projâ‚ = F .map f x
(F âŠ— G) .map f (x , y) .projâ‚‚ = G .map f y
(F âŠ— G) .map-resp-â‰ˆ f (x , y) .projâ‚ = F .map-resp-â‰ˆ f x
(F âŠ— G) .map-resp-â‰ˆ f (x , y) .projâ‚‚ = G .map-resp-â‰ˆ f y
(F âŠ— G) .preserves-âˆ˜ f g (x , y) .projâ‚ = F .preserves-âˆ˜ f g x
(F âŠ— G) .preserves-âˆ˜ f g (x , y) .projâ‚‚ = G .preserves-âˆ˜ f g y
(F âŠ— G) .preserves-id f (x , y) .projâ‚ = F .preserves-id f x
(F âŠ— G) .preserves-id f (x , y) .projâ‚‚ = G .preserves-id f y

Ï€â‚ : âˆ€ {a b} {F : FOApproxSetPSh a} {G : FOApproxSetPSh b} â†’ (F âŠ— G) â‡’ F
Ï€â‚ .at X = projâ‚
Ï€â‚ .at-resp-â‰ˆ X = projâ‚
Ï€â‚ {F = F} .commute {X} f _ = F .obj X .isEquivalence .refl

Ï€â‚‚ : âˆ€ {a b} {F : FOApproxSetPSh a} {G : FOApproxSetPSh b} â†’ (F âŠ— G) â‡’ G
Ï€â‚‚ .at X = projâ‚‚
Ï€â‚‚ .at-resp-â‰ˆ X = projâ‚‚
Ï€â‚‚ {G = G} .commute {X} f _ = G .obj X .isEquivalence .refl

pair : âˆ€ {a b c} {F : FOApproxSetPSh a} {G : FOApproxSetPSh b} {H : FOApproxSetPSh c} â†’ F â‡’ G â†’ F â‡’ H â†’ F â‡’ (G âŠ— H)
pair Î¶ Î· .at X x .projâ‚ = Î¶ .at X x
pair Î¶ Î· .at X x .projâ‚‚ = Î· .at X x
pair Î¶ Î· .at-resp-â‰ˆ X x .projâ‚ = Î¶ .at-resp-â‰ˆ X x
pair Î¶ Î· .at-resp-â‰ˆ X x .projâ‚‚ = Î· .at-resp-â‰ˆ X x
pair Î¶ Î· .commute f x .projâ‚ = Î¶ .commute f x
pair Î¶ Î· .commute f x .projâ‚‚ = Î· .commute f x

-- Sums
_+_ : âˆ€ {a} â†’ FOApproxSetPSh a â†’ FOApproxSetPSh a â†’ FOApproxSetPSh a
(F + G) .obj X = +-setoid (F .obj X) (G .obj X)
(F + G) .map f (injâ‚ x) = injâ‚ (F .map f x)
(F + G) .map f (injâ‚‚ x) = injâ‚‚ (G .map f x)
(F + G) .map-resp-â‰ˆ f {injâ‚ x} {injâ‚ y} = F .map-resp-â‰ˆ f
(F + G) .map-resp-â‰ˆ f {injâ‚‚ x} {injâ‚‚ y} = G .map-resp-â‰ˆ f
(F + G) .preserves-âˆ˜ f g (injâ‚ x) = F .preserves-âˆ˜ f g x
(F + G) .preserves-âˆ˜ f g (injâ‚‚ x) = G .preserves-âˆ˜ f g x
(F + G) .preserves-id f (injâ‚ x) = F .preserves-id f x
(F + G) .preserves-id f (injâ‚‚ x) = G .preserves-id f x

inl : âˆ€ {a} {F G : FOApproxSetPSh a} â†’ F â‡’ (F + G)
inl .at X = injâ‚
inl .at-resp-â‰ˆ X = idâ‚›
inl {F = F} .commute {X} f _ = F .obj X .isEquivalence .refl

inr : âˆ€ {a} {F G : FOApproxSetPSh a} â†’ G â‡’ (F + G)
inr .at X = injâ‚‚
inr .at-resp-â‰ˆ X = idâ‚›
inr {G = G} .commute {X} f _ = G .obj X .isEquivalence .refl

[_,_] : âˆ€ {a} {E F G H : FOApproxSetPSh a} â†’ (E âŠ— F) â‡’ H â†’ (E âŠ— G) â‡’ H â†’ (E âŠ— (F + G)) â‡’ H
[ Î¶ , Î· ] .at X (x , injâ‚ y) = Î¶ .at X (x , y)
[ Î¶ , Î· ] .at X (x , injâ‚‚ y) = Î· .at X (x , y)
[ Î¶ , Î· ] .at-resp-â‰ˆ X {xâ‚ , injâ‚ yâ‚} {xâ‚‚ , injâ‚ yâ‚‚} = Î¶ .at-resp-â‰ˆ X
[ Î¶ , Î· ] .at-resp-â‰ˆ X {xâ‚ , injâ‚‚ yâ‚} {xâ‚‚ , injâ‚‚ yâ‚‚} = Î· .at-resp-â‰ˆ X
[ Î¶ , Î· ] .commute f (x , injâ‚ y) = Î¶ .commute f (x , y)
[ Î¶ , Î· ] .commute f (x , injâ‚‚ y) = Î· .commute f (x , y)

-- Yoneda embedding Y â†¦ Hom(-, Y)
ã‚ˆ : FOApproxSet -> FOApproxSetPSh 0â„“
ã‚ˆ Y .obj X = â‰ƒmâ‚-setoid X Y
ã‚ˆ Y .map f g = g âˆ˜â‚ f
ã‚ˆ Y .map-resp-â‰ˆ f g = âˆ˜â‚-resp-â‰ƒmâ‚ g f
ã‚ˆ Y .preserves-âˆ˜ {X} f g h = â‰ƒmâ‚-setoid X Y .isEquivalence .sym (âˆ˜â‚-assoc h f g)
ã‚ˆ Y .preserves-id {X} f g =
  â‰ƒmâ‚-setoid X Y .isEquivalence .trans
    (â‰¡-to-â‰ˆ (â‰ƒmâ‚-setoid X Y) â‰¡-refl) (â‰¡-to-â‰ˆ (â‰ƒmâ‚-setoid X Y) (cong (Î» g' â†’ g' âˆ˜â‚ f) {_} {g} â‰¡-refl))

-- Functions. (F âŠ— ã‚ˆ X) â‡’ G and ã‚ˆ X â‡’ (F âŠ¸ G) are isomorphic
_âŠ¸_ : âˆ€ {a b} â†’ FOApproxSetPSh a â†’ FOApproxSetPSh b â†’ FOApproxSetPSh (suc (a âŠ” b))
(F âŠ¸ G) .obj X = â‰ƒm-setoid (F âŠ— ã‚ˆ X) G
(F âŠ¸ G) .map f Î· .at X (x , g) = Î· .at X (x , f âˆ˜â‚ g)
(F âŠ¸ G) .map f Î· .at-resp-â‰ˆ X (x , g) =
  Î· .at-resp-â‰ˆ X (x , âˆ˜â‚-resp-â‰ƒmâ‚ {f = f} (â‰ƒmâ‚-setoid _ _ .isEquivalence .refl) g)
(F âŠ¸ G) .map f Î· .commute {Y} {Z} g (x , h) =
  G .obj Y .isEquivalence .trans
    (Î· .at-resp-â‰ˆ Y (F .obj Y .isEquivalence .refl , âˆ˜â‚-assoc f h g)) (Î· .commute g (x , f âˆ˜â‚ h))
(F âŠ¸ G) .map-resp-â‰ˆ f Î· .eqat X (x , g) = Î· .eqat X (x , âˆ˜â‚-resp-â‰ƒmâ‚ f g)
(F âŠ¸ G) .preserves-âˆ˜ {Y} {Z = Z} f g Î· .eqat X {_ , hâ‚} (x , h) =
  Î· .at-resp-â‰ˆ X (
    x ,
    â‰ƒmâ‚-setoid X Z .isEquivalence .trans
      (âˆ˜â‚-assoc f g hâ‚) (âˆ˜â‚-resp-â‰ƒmâ‚ {f = f âˆ˜â‚ g} (â‰ƒmâ‚-setoid Y Z .isEquivalence .refl) h)
  )
(F âŠ¸ G) .preserves-id {Y} {Z} f Î· .eqat X (x , h) =
  Î· .at-resp-â‰ˆ X (x , âˆ˜â‚-resp-â‰ƒmâ‚ {f = f} (â‰ƒmâ‚-setoid Y Z .isEquivalence .refl) h)

eval : âˆ€ {a b} {F : FOApproxSetPSh a} {G : FOApproxSetPSh b} â†’ ((F âŠ¸ G) âŠ— F) â‡’ G
eval .at X (Î· , x) = Î· .at X (x , idâ‚)
eval .at-resp-â‰ˆ X (Î· , x) = Î· .eqat X (x , â‰ƒmâ‚-setoid X _ .isEquivalence .refl)
eval {F = F} {G} .commute {X} {Y} f (Î· , y) =
  G .obj X .isEquivalence .trans
    (Î· .at-resp-â‰ˆ X (F .obj X .isEquivalence .refl ,
     â‰ƒmâ‚-setoid X Y .isEquivalence .trans (âˆ˜â‚-unitáµ£ f) (â‰ƒmâ‚-setoid X Y .isEquivalence .sym (âˆ˜â‚-unitâ‚— f))))
    (Î· .commute f (y , idâ‚))

lambda : âˆ€ {a b c} {F : FOApproxSetPSh a} {G : FOApproxSetPSh b} {H : FOApproxSetPSh c} â†’ (F âŠ— G) â‡’ H â†’ F â‡’ (G âŠ¸ H)
lambda {F = F} Î· .at X x .at Y (y , f) = Î· .at Y (F .map f x , y)
lambda {F = F} Î· .at X x .at-resp-â‰ˆ Y (y , f) =
  Î· .at-resp-â‰ˆ Y (F .map-resp-â‰ˆ f (F .obj X .isEquivalence .refl) , y)
lambda {F = F} {G} {H} Î· .at X x .commute {Y} {Z} f (z , g) =
  H .obj Y .isEquivalence .trans
    (Î· .at-resp-â‰ˆ Y (F .obj Y .isEquivalence .sym (F .preserves-âˆ˜ g f x) , G .obj Y .isEquivalence .refl))
    (Î· .commute f (F .map g x , z))
lambda {F = F} {G} Î· .at-resp-â‰ˆ X x .eqat Y (y , f) = Î· .at-resp-â‰ˆ Y (F .map-resp-â‰ˆ f x , y)
lambda {F = F} {G} Î· .commute {X} {Y} f x .eqat Z (z , g) =
  Î· .at-resp-â‰ˆ Z (
    F .obj Z .isEquivalence .trans
      (F .preserves-âˆ˜ f _ x)
      (F .map-resp-â‰ˆ (âˆ˜â‚-resp-â‰ƒmâ‚ {f = f} (â‰ƒmâ‚-setoid X Y .isEquivalence .refl) g) (F .obj Y .isEquivalence .refl)) ,
    z
  )

-- prove law relating eval and lambda

-- Any old set becomes a constant presheaf
Disc : Set â†’ FOApproxSetPSh 0â„“
Disc A .obj X = setoid A
Disc A .map f = idâ‚›
Disc A .map-resp-â‰ˆ f = idâ‚›
Disc A .preserves-âˆ˜ f g x = â‰¡-refl
Disc A .preserves-id f x = â‰¡-refl

Disc-f : âˆ€ {A B} â†’ (A â†’ B) â†’ Disc A â‡’ Disc B
Disc-f f .at X = f
Disc-f f .at-resp-â‰ˆ X = cong f
Disc-f f .commute g x = â‰¡-refl

Disc-const : âˆ€ {A} â†’ A â†’ âŠ¤ â‡’ Disc A
Disc-const x .at X _ = x
Disc-const x .at-resp-â‰ˆ X _ = â‰¡-refl
Disc-const x .commute f _ = â‰¡-refl

Disc-reflects-products : âˆ€ {A B} â†’ (Disc A âŠ— Disc B) â‡’ Disc (A Ã— B)
Disc-reflects-products .at X = idâ‚›
Disc-reflects-products .at-resp-â‰ˆ X (x , y) = congâ‚‚ _,_ x y
Disc-reflects-products .commute f (x , y) = â‰¡-refl

-- Helper for binary predicate over a set
module _ where
  open import Relation.Binary using (Decidable; Rel)
  open import Relation.Nullary

  binPred : âˆ€ {â„“ A} {_âˆ¼_ : Rel A â„“} â†’ Decidable _âˆ¼_ â†’ Disc (A Ã— A) â‡’ (âŠ¤ + âŠ¤)
  binPred _âˆ¼_ .at X (x , y) with x âˆ¼ y
  ... | yes _ = injâ‚ tt
  ... | no _ = injâ‚‚ tt
  binPred _âˆ¼_ .at-resp-â‰ˆ X {x , y} â‰¡-refl with x âˆ¼ y
  ... | yes _ = â‰¡-refl
  ... | no _ = â‰¡-refl
  binPred _âˆ¼_ .commute f (x , y) with x âˆ¼ y
  ... | yes _ = â‰¡-refl
  ... | no _ = â‰¡-refl

-- Lifting
â„’ : âˆ€ {a} â†’ FOApproxSetPSh a â†’ FOApproxSetPSh a
â„’ F .obj X = F .obj (â„’â‚ X)
â„’ F .map f = F .map (â„’-map f)
â„’ F .map-resp-â‰ˆ f = F .map-resp-â‰ˆ (â„’-map-resp-â‰ƒ f)
â„’ F .preserves-âˆ˜ {X} {Y} {Z} f g x =
  F .obj (â„’â‚ X) .isEquivalence .trans
    (F .preserves-âˆ˜ (â„’-map f) (â„’-map g) x)
    (F .map-resp-â‰ˆ (â„’-map-preserves-âˆ˜ f g) (F .obj (â„’â‚ Z) .isEquivalence .refl))
â„’ F .preserves-id f x = F .preserves-id (â„’-map f) x

â„’-join : âˆ€ {F : FOApproxSetPSh 0â„“} â†’ â„’ (â„’ F) â‡’ â„’ F
â„’-join {F} .at X = F .map â„’â‚-unit
â„’-join {F} .at-resp-â‰ˆ X = F .map-resp-â‰ˆ (â‰ƒmâ‚-setoid (â„’â‚ X) _ .isEquivalence .refl)
â„’-join {F} .commute {X} {Y} f x =
  begin
    F .map â„’â‚-unit (F .map (â„’-map (â„’-map f)) x)
  â‰ˆâŸ¨ F .preserves-âˆ˜ _ _ x âŸ©
    F .map (â„’-map (â„’-map f) âˆ˜â‚ â„’â‚-unit {â„’â‚ X}) x
  â‰ˆâŸ¨ F .map-resp-â‰ˆ (â‰ƒmâ‚-setoid _ _ .isEquivalence .sym (â„’-unit-commute (â„’-map f)))
                   (F .obj (â„’â‚ (â„’â‚ Y)) .isEquivalence .refl) âŸ©
    F .map (â„’â‚-unit {â„’â‚ Y} âˆ˜â‚ â„’-map f) x
  â‰ˆâŸ¨ F .obj (â„’â‚ X) .isEquivalence .sym (F .preserves-âˆ˜ _ _ x) âŸ©
    F .map (â„’-map f) (F .map (â„’â‚-unit {â„’â‚ Y}) x)
  âˆ
  where open import Relation.Binary.Reasoning.Setoid (F .obj (â„’â‚ X))
