{-# OPTIONS --prop --postfix-projections --safe #-}

module additive-category where

open import Level
open import categories
open import prop-setoid using (module â‰ˆ-Reasoning; IsEquivalence)
open import commutative-monoid using (CommutativeMonoid)

record AdditiveCat {o m} (ğ’ : Category o m m) : Set (o âŠ” m) where
  open Category ğ’
  open CommutativeMonoid
  field
    homCM : âˆ€ x y â†’ CommutativeMonoid m (hom-setoid x y)

  _+m_ : âˆ€ {x y} â†’ x â‡’ y â†’ x â‡’ y â†’ x â‡’ y
  f +m g = homCM _ _ ._+_ f g

  Îµm : âˆ€ {x y} â†’ x â‡’ y
  Îµm {x} {y} = homCM x y .Îµ

  field
    comp-bilinearâ‚ : âˆ€ {X Y Z} (fâ‚ fâ‚‚ : Y â‡’ Z) (g : X â‡’ Y) â†’
                     ((fâ‚ +m fâ‚‚) âˆ˜ g) â‰ˆ ((fâ‚ âˆ˜ g) +m (fâ‚‚ âˆ˜ g))
    comp-bilinearâ‚‚ : âˆ€ {X Y Z} (f : Y â‡’ Z) (gâ‚ gâ‚‚ : X â‡’ Y) â†’
                     (f âˆ˜ (gâ‚ +m gâ‚‚)) â‰ˆ ((f âˆ˜ gâ‚) +m (f âˆ˜ gâ‚‚))
    comp-bilinear-Îµâ‚ : âˆ€ {X Y Z} (f : X â‡’ Y) â†’ (Îµm âˆ˜ f) â‰ˆ Îµm {X} {Z}
    comp-bilinear-Îµâ‚‚ : âˆ€ {X Y Z} (f : Y â‡’ Z) â†’ (f âˆ˜ Îµm) â‰ˆ Îµm {X} {Z}

-- Construct biproducts from products on an additive category
module _ {o m} (ğ’ : Category o m m) (Ağ’ : AdditiveCat ğ’) (P : HasProducts ğ’)
    where

  open Category ğ’
  open AdditiveCat Ağ’
  open CommutativeMonoid
  open IsEquivalence
  module P = HasProducts P

  -- Use the universal property of products to show that the pairing
  -- operation preserves addition.
  pair-+ : âˆ€ {x y z} (fâ‚ fâ‚‚ : x â‡’ y) (gâ‚ gâ‚‚ : x â‡’ z) â†’
     (P.pair fâ‚ gâ‚ +m P.pair fâ‚‚ gâ‚‚) â‰ˆ P.pair (fâ‚ +m fâ‚‚) (gâ‚ +m gâ‚‚)
  pair-+ fâ‚ fâ‚‚ gâ‚ gâ‚‚ =
    begin
      P.pair fâ‚ gâ‚ +m P.pair fâ‚‚ gâ‚‚
    â‰ˆâŸ¨ â‰ˆ-sym (P.pair-ext _) âŸ©
      P.pair (P.pâ‚ âˆ˜ (P.pair fâ‚ gâ‚ +m P.pair fâ‚‚ gâ‚‚)) (P.pâ‚‚ âˆ˜ (P.pair fâ‚ gâ‚ +m P.pair fâ‚‚ gâ‚‚))
    â‰ˆâŸ¨ P.pair-cong (comp-bilinearâ‚‚ _ _ _) (comp-bilinearâ‚‚ _ _ _) âŸ©
      P.pair ((P.pâ‚ âˆ˜ P.pair fâ‚ gâ‚) +m (P.pâ‚ âˆ˜ P.pair fâ‚‚ gâ‚‚)) ((P.pâ‚‚ âˆ˜ P.pair fâ‚ gâ‚) +m (P.pâ‚‚ âˆ˜ P.pair fâ‚‚ gâ‚‚))
    â‰ˆâŸ¨ P.pair-cong (homCM _ _ .+-cong (P.pair-pâ‚ _ _) (P.pair-pâ‚ _ _)) (homCM _ _ .+-cong (P.pair-pâ‚‚ _ _) (P.pair-pâ‚‚ _ _)) âŸ©
      P.pair (fâ‚ +m fâ‚‚) (gâ‚ +m gâ‚‚)
    âˆ where open â‰ˆ-Reasoning isEquiv

  _âŠ•_ = P.prod

  inâ‚ : âˆ€ {x y} â†’ x â‡’ (x âŠ• y)
  inâ‚ = P.pair (id _) Îµm

  inâ‚‚ : âˆ€ {x y} â†’ y â‡’ (x âŠ• y)
  inâ‚‚ = P.pair Îµm (id _)

  copair : âˆ€ {x y z} â†’ x â‡’ z â†’ y â‡’ z â†’ (x âŠ• y) â‡’ z
  copair f g = (f âˆ˜ P.pâ‚) +m (g âˆ˜ P.pâ‚‚)

  copair-cong : âˆ€ {x y z} {fâ‚ fâ‚‚ : x â‡’ z} {gâ‚ gâ‚‚ : y â‡’ z} â†’ fâ‚ â‰ˆ fâ‚‚ â†’ gâ‚ â‰ˆ gâ‚‚ â†’ copair fâ‚ gâ‚ â‰ˆ copair fâ‚‚ gâ‚‚
  copair-cong fâ‚â‰ˆfâ‚‚ gâ‚â‰ˆgâ‚‚ = homCM _ _ .+-cong (âˆ˜-cong fâ‚â‰ˆfâ‚‚ â‰ˆ-refl) (âˆ˜-cong gâ‚â‰ˆgâ‚‚ â‰ˆ-refl)

  copair-inâ‚ : âˆ€ {x y z} (f : x â‡’ z) (g : y â‡’ z) â†’ (copair f g âˆ˜ inâ‚) â‰ˆ f
  copair-inâ‚ f g =
    begin
      ((f âˆ˜ P.pâ‚) +m (g âˆ˜ P.pâ‚‚)) âˆ˜ P.pair (id _) Îµm
    â‰ˆâŸ¨ comp-bilinearâ‚ _ _ _ âŸ©
      ((f âˆ˜ P.pâ‚) âˆ˜ P.pair (id _) Îµm) +m ((g âˆ˜ P.pâ‚‚) âˆ˜ P.pair (id _) Îµm)
    â‰ˆâŸ¨ homCM _ _ .+-cong (assoc _ _ _) (assoc _ _ _) âŸ©
      (f âˆ˜ (P.pâ‚ âˆ˜ P.pair (id _) Îµm)) +m (g âˆ˜ (P.pâ‚‚ âˆ˜ P.pair (id _) Îµm))
    â‰ˆâŸ¨ homCM _ _ .+-cong (âˆ˜-cong â‰ˆ-refl (P.pair-pâ‚ _ _)) (âˆ˜-cong â‰ˆ-refl (P.pair-pâ‚‚ _ _)) âŸ©
      (f âˆ˜ id _) +m (g âˆ˜ Îµm)
    â‰ˆâŸ¨ homCM _ _ .+-cong id-right (comp-bilinear-Îµâ‚‚ g) âŸ©
      f  +m Îµm
    â‰ˆâŸ¨ homCM _ _ .+-comm âŸ©
      Îµm  +m f
    â‰ˆâŸ¨ homCM _ _ .+-lunit âŸ©
      f
    âˆ
    where open â‰ˆ-Reasoning isEquiv

  copair-inâ‚‚ : âˆ€ {x y z} (f : x â‡’ z) (g : y â‡’ z) â†’ (copair f g âˆ˜ inâ‚‚) â‰ˆ g
  copair-inâ‚‚ f g =
    begin
      ((f âˆ˜ P.pâ‚) +m (g âˆ˜ P.pâ‚‚)) âˆ˜ P.pair Îµm (id _)
    â‰ˆâŸ¨ comp-bilinearâ‚ _ _ _ âŸ©
      ((f âˆ˜ P.pâ‚) âˆ˜ P.pair Îµm (id _)) +m ((g âˆ˜ P.pâ‚‚) âˆ˜ P.pair Îµm (id _))
    â‰ˆâŸ¨ homCM _ _ .+-cong (assoc _ _ _) (assoc _ _ _) âŸ©
      (f âˆ˜ (P.pâ‚ âˆ˜ P.pair Îµm (id _))) +m (g âˆ˜ (P.pâ‚‚ âˆ˜ P.pair Îµm (id _)))
    â‰ˆâŸ¨ homCM _ _ .+-cong (âˆ˜-cong â‰ˆ-refl (P.pair-pâ‚ _ _)) (âˆ˜-cong â‰ˆ-refl (P.pair-pâ‚‚ _ _)) âŸ©
      (f âˆ˜ Îµm) +m (g âˆ˜ id _)
    â‰ˆâŸ¨ homCM _ _ .+-cong (comp-bilinear-Îµâ‚‚ f) id-right âŸ©
      Îµm  +m g
    â‰ˆâŸ¨ homCM _ _ .+-lunit âŸ©
      g
    âˆ
    where open â‰ˆ-Reasoning isEquiv

  copair-ext : âˆ€ {x y z} (f : (x âŠ• y) â‡’ z) â†’ copair (f âˆ˜ inâ‚) (f âˆ˜ inâ‚‚) â‰ˆ f
  copair-ext f =
    begin
      ((f âˆ˜ P.pair (id _) Îµm) âˆ˜ P.pâ‚) +m ((f âˆ˜ P.pair Îµm (id _)) âˆ˜ P.pâ‚‚)
    â‰ˆâŸ¨ homCM _ _ .+-cong (assoc _ _ _) (assoc _ _ _) âŸ©
      (f âˆ˜ (P.pair (id _) Îµm âˆ˜ P.pâ‚)) +m (f âˆ˜ (P.pair Îµm (id _) âˆ˜ P.pâ‚‚))
    â‰ˆâŸ¨ homCM _ _ .+-cong (âˆ˜-cong â‰ˆ-refl (P.pair-natural _ _ _)) (âˆ˜-cong â‰ˆ-refl (P.pair-natural _ _ _)) âŸ©
      (f âˆ˜ P.pair (id _ âˆ˜ P.pâ‚) (Îµm âˆ˜ P.pâ‚)) +m (f âˆ˜ P.pair (Îµm âˆ˜ P.pâ‚‚) (id _ âˆ˜ P.pâ‚‚))
    â‰ˆâŸ¨ homCM _ _ .+-cong (âˆ˜-cong â‰ˆ-refl (P.pair-cong id-left (comp-bilinear-Îµâ‚ _)))
                         (âˆ˜-cong â‰ˆ-refl (P.pair-cong (comp-bilinear-Îµâ‚ _) id-left)) âŸ©
      (f âˆ˜ P.pair P.pâ‚ Îµm) +m (f âˆ˜ P.pair Îµm P.pâ‚‚)
    â‰ˆâŸ¨ â‰ˆ-sym (comp-bilinearâ‚‚ _ _ _) âŸ©
      f âˆ˜ (P.pair P.pâ‚ Îµm +m P.pair Îµm P.pâ‚‚)
    â‰ˆâŸ¨ âˆ˜-cong â‰ˆ-refl (pair-+ _ _ _ _) âŸ©
      f âˆ˜ P.pair (P.pâ‚ +m Îµm) (Îµm +m P.pâ‚‚)
    â‰ˆâŸ¨ âˆ˜-cong â‰ˆ-refl (P.pair-cong (isEquiv .trans (homCM _ _ .+-comm) (homCM _ _ .+-lunit)) (homCM _ _ .+-lunit)) âŸ©
      f âˆ˜ P.pair P.pâ‚ P.pâ‚‚
    â‰ˆâŸ¨ âˆ˜-cong â‰ˆ-refl P.pair-ext0 âŸ©
      f âˆ˜ id _
    â‰ˆâŸ¨ id-right âŸ©
      f
    âˆ where open â‰ˆ-Reasoning isEquiv

  coproducts : HasCoproducts ğ’
  coproducts .HasCoproducts.coprod = P.prod
  coproducts .HasCoproducts.inâ‚ = inâ‚
  coproducts .HasCoproducts.inâ‚‚ = inâ‚‚
  coproducts .HasCoproducts.copair = copair
  coproducts .HasCoproducts.copair-cong = copair-cong
  coproducts .HasCoproducts.copair-inâ‚ = copair-inâ‚
  coproducts .HasCoproducts.copair-inâ‚‚ = copair-inâ‚‚
  coproducts .HasCoproducts.copair-ext = copair-ext
