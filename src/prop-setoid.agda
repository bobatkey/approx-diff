{-# OPTIONS --prop --postfix-projections --safe #-}

module prop-setoid where

open import Level
open import prop
open import Data.Unit using (tt) renaming (âŠ¤ to ğŸ™S)
open import Data.Sum using (_âŠ_; injâ‚; injâ‚‚)
open import Data.Empty using () renaming (âŠ¥ to ğŸ˜)
open import Data.Product using (_Ã—_; projâ‚; projâ‚‚; _,_)

record IsEquivalence {o e} {A : Set o} (_â‰ˆ_ : A â†’ A â†’ Prop e) : Set (o âŠ” e) where
  field
    refl : âˆ€ {x} â†’ x â‰ˆ x
    sym  : âˆ€ {x y} â†’ x â‰ˆ y â†’ y â‰ˆ x
    trans : âˆ€ {x y z} â†’ x â‰ˆ y â†’ y â‰ˆ z â†’ x â‰ˆ z
open IsEquivalence

module â‰ˆ-Reasoning {o e} {A : Set o} {_â‰ˆ_ : A â†’ A â†’ Prop e} (equiv : IsEquivalence _â‰ˆ_) where

  infix  1 begin_
  infixr 2 _â‰ˆâŸ¨_âŸ©_
  infix  3 _âˆ

  begin_ : âˆ€ {x y : A}
    â†’ x â‰ˆ y
      -----
    â†’ x â‰ˆ y
  begin xâ‰ˆy  =  xâ‰ˆy

  _â‰ˆâŸ¨_âŸ©_ : âˆ€ (x : A) {y z : A}
    â†’ x â‰ˆ y
    â†’ y â‰ˆ z
      -----
    â†’ x â‰ˆ z
  x â‰ˆâŸ¨ xâ‰ˆy âŸ© yâ‰ˆz  =  equiv .trans xâ‰ˆy yâ‰ˆz

  _âˆ : âˆ€ (x : A)
      -----
    â†’ x â‰ˆ x
  x âˆ  =  equiv .refl

record Setoid o e : Set (suc (o âŠ” e)) where
  no-eta-equality
  field
    Carrier : Set o
    _â‰ˆ_     : Carrier â†’ Carrier â†’ Prop e
    isEquivalence : IsEquivalence _â‰ˆ_
  open IsEquivalence isEquivalence public
open Setoid

-- liftSetoid : âˆ€ {o e} o' e' â†’ Setoid o e â†’ Setoid (o âŠ” o') (e âŠ” e')
-- liftSetoid o' e' x .Carrier = {!!}
-- liftSetoid o' e' x ._â‰ˆ_ = {!!}
-- liftSetoid o' e' x .isEquivalence = {!!}

record _â‡’_ {oâ‚ eâ‚ oâ‚‚ eâ‚‚} (X : Setoid oâ‚ eâ‚) (Y : Setoid oâ‚‚ eâ‚‚) : Set (oâ‚ âŠ” oâ‚‚ âŠ” eâ‚ âŠ” eâ‚‚) where
  no-eta-equality
  private
    module X = Setoid X
    module Y = Setoid Y
  field
    func : X.Carrier â†’ Y.Carrier
    func-resp-â‰ˆ : âˆ€ {xâ‚ xâ‚‚} â†’ xâ‚ X.â‰ˆ xâ‚‚ â†’ func xâ‚ Y.â‰ˆ func xâ‚‚
open _â‡’_

module _ {o e} where

  record _â‰ƒm_ {X Y : Setoid o e} (f g : X â‡’ Y) : Prop (o âŠ” e) where
    private
      module X = Setoid X
      module Y = Setoid Y
    field
      func-eq : âˆ€ {xâ‚ xâ‚‚} â†’ xâ‚ X.â‰ˆ xâ‚‚ â†’ f .func xâ‚ Y.â‰ˆ g .func xâ‚‚
  open _â‰ƒm_

  â‰ƒm-isEquivalence : âˆ€ {X Y : Setoid o e} â†’ IsEquivalence (_â‰ƒm_ {X} {Y})
  â‰ƒm-isEquivalence {X} {Y} .refl {f} .func-eq = f .func-resp-â‰ˆ
  â‰ƒm-isEquivalence {X} {Y} .sym {f} {g} fâ‰ˆg .func-eq xâ‚â‰ˆxâ‚‚ = Y .sym (fâ‰ˆg .func-eq (X .sym xâ‚â‰ˆxâ‚‚))
  â‰ƒm-isEquivalence {X} {Y} .trans {f} {g} {h} fâ‰ˆg gâ‰ˆh .func-eq xâ‚â‰ˆxâ‚‚ = Y .trans (fâ‰ˆg .func-eq xâ‚â‰ˆxâ‚‚) (gâ‰ˆh .func-eq (X .refl))

  idS : âˆ€ (X : Setoid o e) â†’ X â‡’ X
  idS X .func x = x
  idS X .func-resp-â‰ˆ e = e

  _âˆ˜S_ : âˆ€ {X Y Z : Setoid o e} â†’ Y â‡’ Z â†’ X â‡’ Y â†’ X â‡’ Z
  (f âˆ˜S g) .func x = f .func (g .func x)
  (f âˆ˜S g) .func-resp-â‰ˆ e = f .func-resp-â‰ˆ (g .func-resp-â‰ˆ e)

  âˆ˜S-cong : âˆ€ {X Y Z : Setoid o e} {fâ‚ fâ‚‚ : Y â‡’ Z} {gâ‚ gâ‚‚ : X â‡’ Y} â†’
    fâ‚ â‰ƒm fâ‚‚  â†’ gâ‚ â‰ƒm gâ‚‚ â†’ (fâ‚ âˆ˜S gâ‚) â‰ƒm (fâ‚‚ âˆ˜S gâ‚‚)
  âˆ˜S-cong fâ‚â‰ˆfâ‚‚ gâ‚â‰ˆgâ‚‚ .func-eq xâ‚â‰ˆxâ‚‚ =
    fâ‚â‰ˆfâ‚‚ .func-eq (gâ‚â‰ˆgâ‚‚ .func-eq xâ‚â‰ˆxâ‚‚)

  id-left : âˆ€ {X Y : Setoid o e} {f : X â‡’ Y} â†’ (idS Y âˆ˜S f) â‰ƒm f
  id-left {X} {Y} {f} .func-eq = f .func-resp-â‰ˆ

  id-right : âˆ€ {X Y : Setoid o e} {f : X â‡’ Y} â†’ (f âˆ˜S idS X) â‰ƒm f
  id-right {X} {Y} {f} .func-eq = f .func-resp-â‰ˆ

  assoc : âˆ€ {W X Y Z : Setoid o e} (f : Y â‡’ Z) (g : X â‡’ Y) (h : W â‡’ X) â†’
      ((f âˆ˜S g) âˆ˜S h) â‰ƒm (f âˆ˜S (g âˆ˜S h))
  assoc f g h .func-eq wâ‚â‰ˆwâ‚‚ = f .func-resp-â‰ˆ (g .func-resp-â‰ˆ (h .func-resp-â‰ˆ wâ‚â‰ˆwâ‚‚))

module _ {o e} where

  ğŸ™ : Setoid o e
  ğŸ™ .Carrier = Lift _ ğŸ™S
  ğŸ™ ._â‰ˆ_ _ _ = âŠ¤
  ğŸ™ .isEquivalence .refl = tt
  ğŸ™ .isEquivalence .sym _ = tt
  ğŸ™ .isEquivalence .trans _ _ = tt

  to-ğŸ™ : âˆ€ {X : Setoid o e} â†’ X â‡’ ğŸ™
  to-ğŸ™ .func _ = lift tt
  to-ğŸ™ .func-resp-â‰ˆ _ = tt

  to-ğŸ™-unique : âˆ€ {X : Setoid o e} (f g : X â‡’ ğŸ™) â†’ f â‰ƒm g
  to-ğŸ™-unique f g ._â‰ƒm_.func-eq _ = tt

  const : (X : Setoid o e) â†’ X .Carrier â†’ ğŸ™ â‡’ X
  const X x .func _ = x
  const X x .func-resp-â‰ˆ tt = X .refl

+-setoid : âˆ€ {a b c d} (X : Setoid a b) (Y : Setoid c d) â†’ Setoid (a âŠ” c) (b âŠ” d)
+-setoid X Y .Carrier = X .Carrier âŠ Y .Carrier
+-setoid {a} {b} {c} {d} X Y ._â‰ˆ_ (injâ‚ x) (injâ‚ y) = LiftP (b âŠ” d) (X ._â‰ˆ_ x y)
+-setoid {a} {b} {c} {d} X Y ._â‰ˆ_ (injâ‚‚ x) (injâ‚‚ y) = LiftP (b âŠ” d) (Y ._â‰ˆ_ x y)
+-setoid X Y ._â‰ˆ_ (injâ‚ x) (injâ‚‚ y) = âŠ¥
+-setoid X Y ._â‰ˆ_ (injâ‚‚ x) (injâ‚ y) = âŠ¥
+-setoid X Y .isEquivalence .refl {injâ‚ x} .lower = X .isEquivalence .refl
+-setoid X Y .isEquivalence .refl {injâ‚‚ x} .lower = Y .isEquivalence .refl
+-setoid X Y .isEquivalence .sym {injâ‚ x} {injâ‚ y} xâ‰ˆy .lower = X .isEquivalence .sym (xâ‰ˆy .lower)
+-setoid X Y .isEquivalence .sym {injâ‚‚ x} {injâ‚‚ y} xâ‰ˆy .lower = Y .isEquivalence .sym (xâ‰ˆy .lower)
+-setoid X Y .isEquivalence .trans {injâ‚ x} {injâ‚ y} {injâ‚ z} xâ‰ˆy yâ‰ˆz .lower =
    X .isEquivalence .trans (xâ‰ˆy .lower) (yâ‰ˆz .lower)
+-setoid X Y .isEquivalence .trans {injâ‚‚ x} {injâ‚‚ y} {injâ‚‚ z} xâ‰ˆy yâ‰ˆz .lower =
    Y .isEquivalence .trans (xâ‰ˆy .lower) (yâ‰ˆz .lower)

injectâ‚ : âˆ€ {o e} {X Y : Setoid o e} â†’ X â‡’ +-setoid X Y
injectâ‚ .func = injâ‚
injectâ‚ .func-resp-â‰ˆ = lift

injectâ‚‚ : âˆ€ {o e} {X Y : Setoid o e} â†’ Y â‡’ +-setoid X Y
injectâ‚‚ .func = injâ‚‚
injectâ‚‚ .func-resp-â‰ˆ = lift

copair : âˆ€ {o e} {X Y Z : Setoid o e} â†’ X â‡’ Z â†’ Y â‡’ Z â†’ +-setoid X Y â‡’ Z
copair f g .func (injâ‚ x) = f .func x
copair f g .func (injâ‚‚ y) = g .func y
copair f g .func-resp-â‰ˆ {injâ‚ x} {injâ‚ xâ‚} (lift e) = f .func-resp-â‰ˆ e
copair f g .func-resp-â‰ˆ {injâ‚‚ y} {injâ‚‚ yâ‚} (lift e) = g .func-resp-â‰ˆ e

âŠ—-setoid : âˆ€ {a b c d} â†’ Setoid a b â†’ Setoid c d â†’ Setoid (a âŠ” c) (b âŠ” d)
âŠ—-setoid X Y .Carrier = X .Carrier Ã— Y .Carrier
âŠ—-setoid X Y ._â‰ˆ_ (xâ‚ , yâ‚) (xâ‚‚ , yâ‚‚) = X ._â‰ˆ_ xâ‚ xâ‚‚ âˆ§ Y ._â‰ˆ_ yâ‚ yâ‚‚
âŠ—-setoid X Y .isEquivalence .refl .projâ‚ = X .isEquivalence .refl
âŠ—-setoid X Y .isEquivalence .refl .projâ‚‚ = Y .isEquivalence .refl
âŠ—-setoid X Y .isEquivalence .sym (xâ‚â‰ˆyâ‚ , _) .projâ‚ = X .isEquivalence .sym xâ‚â‰ˆyâ‚
âŠ—-setoid X Y .isEquivalence .sym (_ , xâ‚‚â‰ˆyâ‚‚) .projâ‚‚ = Y .isEquivalence .sym xâ‚‚â‰ˆyâ‚‚
âŠ—-setoid X Y .isEquivalence .trans (xâ‚â‰ˆyâ‚ , _) (yâ‚â‰ˆzâ‚ , _) .projâ‚ = X .isEquivalence .trans xâ‚â‰ˆyâ‚ yâ‚â‰ˆzâ‚
âŠ—-setoid X Y .isEquivalence .trans (_ , xâ‚‚â‰ˆyâ‚‚) (_ , yâ‚‚â‰ˆzâ‚‚) .projâ‚‚ = Y .isEquivalence .trans xâ‚‚â‰ˆyâ‚‚ yâ‚‚â‰ˆzâ‚‚

projectâ‚ : âˆ€ {o e} {X Y : Setoid o e} â†’ âŠ—-setoid X Y â‡’ X
projectâ‚ .func = projâ‚
projectâ‚ .func-resp-â‰ˆ = projâ‚

projectâ‚‚ : âˆ€ {o e} {X Y : Setoid o e} â†’ âŠ—-setoid X Y â‡’ Y
projectâ‚‚ .func = projâ‚‚
projectâ‚‚ .func-resp-â‰ˆ = projâ‚‚

pair : âˆ€ {o e} {X Y Z : Setoid o e} â†’ X â‡’ Y â†’ X â‡’ Z â†’ X â‡’ âŠ—-setoid Y Z
pair f g .func x = f .func x , g .func x
pair f g .func-resp-â‰ˆ e = f .func-resp-â‰ˆ e , g .func-resp-â‰ˆ e

open _â‰ƒm_

pair-cong : âˆ€ {o e} {X Y Z : Setoid o e} {fâ‚ fâ‚‚ : X â‡’ Y} {gâ‚ gâ‚‚ : X â‡’ Z} â†’
  fâ‚ â‰ƒm fâ‚‚ â†’ gâ‚ â‰ƒm gâ‚‚ â†’ pair fâ‚ gâ‚ â‰ƒm pair fâ‚‚ gâ‚‚
pair-cong fâ‚â‰ˆfâ‚‚ gâ‚â‰ˆgâ‚‚ .func-eq xâ‚â‰ˆxâ‚‚ .projâ‚ = fâ‚â‰ˆfâ‚‚ .func-eq xâ‚â‰ˆxâ‚‚
pair-cong fâ‚â‰ˆfâ‚‚ gâ‚â‰ˆgâ‚‚ .func-eq xâ‚â‰ˆxâ‚‚ .projâ‚‚ = gâ‚â‰ˆgâ‚‚ .func-eq xâ‚â‰ˆxâ‚‚

-- Strong coproducts
case : âˆ€ {o e} {W X Y Z : Setoid o e} â†’
          (âŠ—-setoid W X) â‡’ Z â†’
          (âŠ—-setoid W Y) â‡’ Z â†’
          (âŠ—-setoid W (+-setoid X Y)) â‡’ Z
case f g .func (w , injâ‚ x) = f .func (w , x)
case f g .func (w , injâ‚‚ y) = g .func (w , y)
case f g .func-resp-â‰ˆ {wâ‚ , injâ‚ xâ‚} {wâ‚‚ , injâ‚ xâ‚‚} (wâ‚â‰ˆwâ‚‚ , lift xâ‚â‰ˆxâ‚‚) = f .func-resp-â‰ˆ (wâ‚â‰ˆwâ‚‚ , xâ‚â‰ˆxâ‚‚)
case f g .func-resp-â‰ˆ {wâ‚ , injâ‚‚ yâ‚} {wâ‚‚ , injâ‚‚ yâ‚‚} (wâ‚â‰ˆwâ‚‚ , lift yâ‚â‰ˆyâ‚‚) = g .func-resp-â‰ˆ (wâ‚â‰ˆwâ‚‚ , yâ‚â‰ˆyâ‚‚)
