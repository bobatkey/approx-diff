{-# OPTIONS --prop --postfix-projections --safe #-}

open import Level using (_âŠ”_; 0â„“)
open import prop-setoid using (module â‰ˆ-Reasoning)
open import categories using (Category; IsTerminal; IsProduct; IsProduct-cong; HasProducts)
open import functor using (Functor; preserve-limits-of-shape; IsLimit;
                           NatIso; NatTrans; â‰ƒ-NatTrans; constFmor)

module finite-product-functor
  {oâ‚ mâ‚ eâ‚ oâ‚‚ mâ‚‚ eâ‚‚} {ğ’ : Category oâ‚ mâ‚ eâ‚} {ğ’Ÿ : Category oâ‚‚ mâ‚‚ eâ‚‚}
  (F : Functor ğ’ ğ’Ÿ)
  where

private
  module ğ’ = Category ğ’
  module ğ’Ÿ = Category ğ’Ÿ
  module F = Functor F

record FPFunctor : Set (oâ‚ âŠ” mâ‚ âŠ” eâ‚ âŠ” oâ‚‚ âŠ” mâ‚‚ âŠ” eâ‚‚) where
  field
    preserve-terminal : âˆ€ t â†’ IsTerminal ğ’ t â†’ IsTerminal ğ’Ÿ (F.fobj t)
    preserve-products : âˆ€ (x y xy : ğ’.obj)
      (pâ‚ : xy ğ’.â‡’ x) (pâ‚‚ : xy ğ’.â‡’ y) â†’
      IsProduct ğ’ x y xy pâ‚ pâ‚‚ â†’
      IsProduct ğ’Ÿ (F.fobj x) (F.fobj y) (F.fobj xy) (F.fmor pâ‚) (F.fmor pâ‚‚)

-- If a functor preserves the empty diagram, then it preserves terminal objects

open import empty-diagram using (IsLimitâ†’IsTerminal; terminalâ†’limit)
open import product-diagram using (IsLimitâ†’IsProduct; productâ†’limit)

mk-preserve-terminal : preserve-limits-of-shape empty-diagram.cat F â†’
                       âˆ€ t â†’ IsTerminal ğ’ t â†’ IsTerminal ğ’Ÿ (F.fobj t)
mk-preserve-terminal F-preserve t t-terminal =
    IsLimitâ†’IsTerminal ğ’Ÿ (F-preserve _ t _ (terminalâ†’limit ğ’ t-terminal))

-- If a functor preserves product-shaped limits, then it preserves products

mk-preserve-products : preserve-limits-of-shape product-diagram.cat F â†’
                       âˆ€ (x y xy : ğ’.obj) (pâ‚ : xy ğ’.â‡’ x) (pâ‚‚ : xy ğ’.â‡’ y) â†’
                       IsProduct ğ’ x y xy pâ‚ pâ‚‚ â†’
                       IsProduct ğ’Ÿ (F.fobj x) (F.fobj y) (F.fobj xy) (F.fmor pâ‚) (F.fmor pâ‚‚)
mk-preserve-products F-preserve x y xy pâ‚ pâ‚‚ is-product =
  IsProduct-cong ğ’Ÿ (ğ’Ÿ.â‰ˆ-trans ğ’Ÿ.id-right ğ’Ÿ.id-left) (ğ’Ÿ.â‰ˆ-trans ğ’Ÿ.id-right ğ’Ÿ.id-left) (IsLimitâ†’IsProduct ğ’Ÿ lim)
  where lim = F-preserve _ xy _ (productâ†’limit ğ’ is-product)

preserve-fpâ†’FPFunctor : preserve-limits-of-shape empty-diagram.cat F â†’
                        preserve-limits-of-shape product-diagram.cat F â†’
                        FPFunctor
preserve-fpâ†’FPFunctor preserve-empty preserve-product .FPFunctor.preserve-terminal = mk-preserve-terminal preserve-empty
preserve-fpâ†’FPFunctor preserve-empty preserve-product .FPFunctor.preserve-products = mk-preserve-products preserve-product

continuousâ†’FPFunctor : (âˆ€ (ğ’® : Category 0â„“ 0â„“ 0â„“) â†’ preserve-limits-of-shape ğ’® F) â†’
                       FPFunctor
continuousâ†’FPFunctor preserve-all = preserve-fpâ†’FPFunctor (preserve-all _) (preserve-all _)

module _ (ğ’P : HasProducts ğ’) (ğ’ŸP : HasProducts ğ’Ÿ) where

  private
    module ğ’P = HasProducts ğ’P
    module ğ’ŸP = HasProducts ğ’ŸP

  preserve-chosen-products : Set (oâ‚ âŠ” mâ‚‚ âŠ” eâ‚‚)
  preserve-chosen-products = âˆ€ {x y} â†’ ğ’Ÿ.IsIso (ğ’ŸP.pair (F.fmor (ğ’P.pâ‚ {x} {y})) (F.fmor (ğ’P.pâ‚‚ {x} {y})))

  module preserve-chosen-products-consequences (Fp : preserve-chosen-products) where

    mul : âˆ€ {x y} â†’ ğ’ŸP.prod (F.fobj x) (F.fobj y) ğ’Ÿ.â‡’ F.fobj (ğ’P.prod x y)
    mul = Fp .ğ’Ÿ.IsIso.inverse

    mulâ»Â¹ : âˆ€ {x y} â†’ F.fobj (ğ’P.prod x y) ğ’Ÿ.â‡’ ğ’ŸP.prod (F.fobj x) (F.fobj y)
    mulâ»Â¹ {x} {y} = ğ’ŸP.pair (F.fmor (ğ’P.pâ‚ {x} {y})) (F.fmor (ğ’P.pâ‚‚ {x} {y}))

    mul-inv : âˆ€ {x y} â†’ (mul {x} {y} ğ’Ÿ.âˆ˜ mulâ»Â¹) ğ’Ÿ.â‰ˆ ğ’Ÿ.id _
    mul-inv = Category.IsIso.inverseâˆ˜fâ‰ˆid Fp

    mulâ»Â¹-natural : âˆ€ {x x' y y'} {f : x ğ’.â‡’ x'} {g : y ğ’.â‡’ y'} â†’
                  (mulâ»Â¹ ğ’Ÿ.âˆ˜ F.fmor (ğ’P.prod-m f g)) ğ’Ÿ.â‰ˆ (ğ’ŸP.prod-m (F.fmor f) (F.fmor g) ğ’Ÿ.âˆ˜ mulâ»Â¹)
    mulâ»Â¹-natural {x} {x'} {y} {y'} {f} {g} = begin
        mulâ»Â¹ ğ’Ÿ.âˆ˜ F.fmor (ğ’P.prod-m f g)
      â‰¡âŸ¨âŸ©
        ğ’ŸP.pair (F.fmor ğ’P.pâ‚) (F.fmor ğ’P.pâ‚‚) ğ’Ÿ.âˆ˜ F.fmor (ğ’P.prod-m f g)
      â‰ˆâŸ¨ ğ’ŸP.pair-natural _ _ _ âŸ©
        ğ’ŸP.pair (F.fmor ğ’P.pâ‚ ğ’Ÿ.âˆ˜ F.fmor (ğ’P.prod-m f g)) (F.fmor ğ’P.pâ‚‚ ğ’Ÿ.âˆ˜ F.fmor (ğ’P.prod-m f g))
      â‰ˆË˜âŸ¨ ğ’ŸP.pair-cong (F.fmor-comp _ _) (F.fmor-comp _ _) âŸ©
        ğ’ŸP.pair (F.fmor (ğ’P.pâ‚ ğ’.âˆ˜ ğ’P.prod-m f g)) (F.fmor (ğ’P.pâ‚‚ ğ’.âˆ˜ ğ’P.prod-m f g))
      â‰ˆâŸ¨ ğ’ŸP.pair-cong (F.fmor-cong (ğ’P.pair-pâ‚ _ _)) (F.fmor-cong (ğ’P.pair-pâ‚‚ _ _)) âŸ©
        ğ’ŸP.pair (F.fmor (f ğ’.âˆ˜ ğ’P.pâ‚)) (F.fmor (g ğ’.âˆ˜ ğ’P.pâ‚‚))
      â‰ˆâŸ¨ ğ’ŸP.pair-cong (F.fmor-comp _ _) (F.fmor-comp _ _) âŸ©
        ğ’ŸP.pair (F.fmor f ğ’Ÿ.âˆ˜ F.fmor ğ’P.pâ‚) (F.fmor g ğ’Ÿ.âˆ˜ F.fmor ğ’P.pâ‚‚)
      â‰ˆË˜âŸ¨ ğ’ŸP.pair-compose _ _ _ _ âŸ©
        ğ’ŸP.prod-m (F.fmor f) (F.fmor g) ğ’Ÿ.âˆ˜ ğ’ŸP.pair (F.fmor ğ’P.pâ‚) (F.fmor ğ’P.pâ‚‚)
      â‰¡âŸ¨âŸ©
        ğ’ŸP.prod-m (F.fmor f) (F.fmor g) ğ’Ÿ.âˆ˜ mulâ»Â¹
      âˆ
      where open â‰ˆ-Reasoning ğ’Ÿ.isEquiv

    mul-natural : âˆ€ {x x' y y'} {f : x ğ’.â‡’ x'} {g : y ğ’.â‡’ y'} â†’
                  (F.fmor (ğ’P.prod-m f g) ğ’Ÿ.âˆ˜ mul) ğ’Ÿ.â‰ˆ (mul ğ’Ÿ.âˆ˜ ğ’ŸP.prod-m (F.fmor f) (F.fmor g))
    mul-natural {x} {x'} {y} {y'} {f} {g} = begin
        F.fmor (ğ’P.prod-m f g) ğ’Ÿ.âˆ˜ mul
      â‰ˆË˜âŸ¨ ğ’Ÿ.id-left âŸ©
        ğ’Ÿ.id _ ğ’Ÿ.âˆ˜ (F.fmor (ğ’P.prod-m f g) ğ’Ÿ.âˆ˜ mul)
      â‰ˆË˜âŸ¨ ğ’Ÿ.âˆ˜-cong (Fp .ğ’Ÿ.IsIso.inverseâˆ˜fâ‰ˆid) ğ’Ÿ.â‰ˆ-refl âŸ©
        (mul ğ’Ÿ.âˆ˜ mulâ»Â¹) ğ’Ÿ.âˆ˜ (F.fmor (ğ’P.prod-m f g) ğ’Ÿ.âˆ˜ mul)
      â‰ˆâŸ¨ ğ’Ÿ.assoc _ _ _ âŸ©
        mul ğ’Ÿ.âˆ˜ (mulâ»Â¹ ğ’Ÿ.âˆ˜ (F.fmor (ğ’P.prod-m f g) ğ’Ÿ.âˆ˜ mul))
      â‰ˆË˜âŸ¨ ğ’Ÿ.âˆ˜-cong ğ’Ÿ.â‰ˆ-refl (ğ’Ÿ.assoc _ _ _) âŸ©
        mul ğ’Ÿ.âˆ˜ ((mulâ»Â¹ ğ’Ÿ.âˆ˜ F.fmor (ğ’P.prod-m f g)) ğ’Ÿ.âˆ˜ mul)
      â‰ˆâŸ¨ ğ’Ÿ.âˆ˜-cong ğ’Ÿ.â‰ˆ-refl (ğ’Ÿ.âˆ˜-cong mulâ»Â¹-natural ğ’Ÿ.â‰ˆ-refl) âŸ©
        mul ğ’Ÿ.âˆ˜ ((ğ’ŸP.prod-m (F.fmor f) (F.fmor g) ğ’Ÿ.âˆ˜ mulâ»Â¹) ğ’Ÿ.âˆ˜ mul)
      â‰ˆâŸ¨ ğ’Ÿ.âˆ˜-cong ğ’Ÿ.â‰ˆ-refl (ğ’Ÿ.assoc _ _ _) âŸ©
        mul ğ’Ÿ.âˆ˜ (ğ’ŸP.prod-m (F.fmor f) (F.fmor g) ğ’Ÿ.âˆ˜ (mulâ»Â¹ ğ’Ÿ.âˆ˜ mul))
      â‰ˆâŸ¨ ğ’Ÿ.âˆ˜-cong ğ’Ÿ.â‰ˆ-refl (ğ’Ÿ.âˆ˜-cong ğ’Ÿ.â‰ˆ-refl (Fp .ğ’Ÿ.IsIso.fâˆ˜inverseâ‰ˆid)) âŸ©
        mul ğ’Ÿ.âˆ˜ (ğ’ŸP.prod-m (F.fmor f) (F.fmor g) ğ’Ÿ.âˆ˜ ğ’Ÿ.id _)
      â‰ˆâŸ¨ ğ’Ÿ.âˆ˜-cong ğ’Ÿ.â‰ˆ-refl ğ’Ÿ.id-right âŸ©
        mul ğ’Ÿ.âˆ˜ ğ’ŸP.prod-m (F.fmor f) (F.fmor g)
      âˆ
      where open â‰ˆ-Reasoning ğ’Ÿ.isEquiv

    F-pâ‚ : âˆ€ {x y} â†’ (F.fmor (ğ’P.pâ‚ {x} {y}) ğ’Ÿ.âˆ˜ mul) ğ’Ÿ.â‰ˆ ğ’ŸP.pâ‚
    F-pâ‚ {x} {y} = begin
        F.fmor (ğ’P.pâ‚ {x} {y}) ğ’Ÿ.âˆ˜ mul
      â‰ˆË˜âŸ¨ ğ’Ÿ.âˆ˜-cong (ğ’ŸP.pair-pâ‚ _ _) ğ’Ÿ.â‰ˆ-refl âŸ©
        (ğ’ŸP.pâ‚ ğ’Ÿ.âˆ˜ mulâ»Â¹) ğ’Ÿ.âˆ˜ mul
      â‰ˆâŸ¨ ğ’Ÿ.assoc _ _ _ âŸ©
        ğ’ŸP.pâ‚ ğ’Ÿ.âˆ˜ (mulâ»Â¹ ğ’Ÿ.âˆ˜ mul)
      â‰ˆâŸ¨ ğ’Ÿ.âˆ˜-cong ğ’Ÿ.â‰ˆ-refl (Fp .ğ’Ÿ.IsIso.fâˆ˜inverseâ‰ˆid) âŸ©
        ğ’ŸP.pâ‚ ğ’Ÿ.âˆ˜ ğ’Ÿ.id _
      â‰ˆâŸ¨ ğ’Ÿ.id-right âŸ©
        ğ’ŸP.pâ‚
      âˆ
      where open â‰ˆ-Reasoning ğ’Ÿ.isEquiv

    F-pâ‚' : âˆ€ {x y} â†’ F.fmor (ğ’P.pâ‚ {x} {y}) ğ’Ÿ.â‰ˆ (ğ’ŸP.pâ‚ ğ’Ÿ.âˆ˜ mulâ»Â¹)
    F-pâ‚' {x} {y} = ğ’Ÿ.â‰ˆ-sym (ğ’ŸP.pair-pâ‚ _ _)

    F-pâ‚‚ : âˆ€ {x y} â†’ (F.fmor (ğ’P.pâ‚‚ {x} {y}) ğ’Ÿ.âˆ˜ mul) ğ’Ÿ.â‰ˆ ğ’ŸP.pâ‚‚
    F-pâ‚‚ {x} {y} = begin
        F.fmor (ğ’P.pâ‚‚ {x} {y}) ğ’Ÿ.âˆ˜ mul
      â‰ˆË˜âŸ¨ ğ’Ÿ.âˆ˜-cong (ğ’ŸP.pair-pâ‚‚ _ _) ğ’Ÿ.â‰ˆ-refl âŸ©
        (ğ’ŸP.pâ‚‚ ğ’Ÿ.âˆ˜ mulâ»Â¹) ğ’Ÿ.âˆ˜ mul
      â‰ˆâŸ¨ ğ’Ÿ.assoc _ _ _ âŸ©
        ğ’ŸP.pâ‚‚ ğ’Ÿ.âˆ˜ (mulâ»Â¹ ğ’Ÿ.âˆ˜ mul)
      â‰ˆâŸ¨ ğ’Ÿ.âˆ˜-cong ğ’Ÿ.â‰ˆ-refl (Fp .ğ’Ÿ.IsIso.fâˆ˜inverseâ‰ˆid) âŸ©
        ğ’ŸP.pâ‚‚ ğ’Ÿ.âˆ˜ ğ’Ÿ.id _
      â‰ˆâŸ¨ ğ’Ÿ.id-right âŸ©
        ğ’ŸP.pâ‚‚
      âˆ
      where open â‰ˆ-Reasoning ğ’Ÿ.isEquiv

    F-pair : âˆ€ {x y z} {fâ‚ : x ğ’.â‡’ y} {fâ‚‚ : x ğ’.â‡’ z} â†’
            (mul ğ’Ÿ.âˆ˜ ğ’ŸP.pair (F.fmor fâ‚) (F.fmor fâ‚‚)) ğ’Ÿ.â‰ˆ F.fmor (ğ’P.pair fâ‚ fâ‚‚)
    F-pair {x} {y} {z} {fâ‚} {fâ‚‚} = begin
        mul ğ’Ÿ.âˆ˜ ğ’ŸP.pair (F.fmor fâ‚) (F.fmor fâ‚‚)
      â‰ˆË˜âŸ¨ ğ’Ÿ.âˆ˜-cong ğ’Ÿ.â‰ˆ-refl (ğ’ŸP.pair-cong (F.fmor-cong (ğ’P.pair-pâ‚ _ _)) (F.fmor-cong (ğ’P.pair-pâ‚‚ _ _))) âŸ©
        mul ğ’Ÿ.âˆ˜ (ğ’ŸP.pair (F.fmor (ğ’P.pâ‚ ğ’.âˆ˜ ğ’P.pair fâ‚ fâ‚‚)) (F.fmor (ğ’P.pâ‚‚ ğ’.âˆ˜ ğ’P.pair fâ‚ fâ‚‚)))
      â‰ˆâŸ¨ ğ’Ÿ.âˆ˜-cong ğ’Ÿ.â‰ˆ-refl (ğ’ŸP.pair-cong (F.fmor-comp _ _) (F.fmor-comp _ _)) âŸ©
        mul ğ’Ÿ.âˆ˜ (ğ’ŸP.pair (F.fmor ğ’P.pâ‚ ğ’Ÿ.âˆ˜ F.fmor (ğ’P.pair fâ‚ fâ‚‚)) (F.fmor ğ’P.pâ‚‚ ğ’Ÿ.âˆ˜ F.fmor (ğ’P.pair fâ‚ fâ‚‚)))
      â‰ˆË˜âŸ¨ ğ’Ÿ.âˆ˜-cong ğ’Ÿ.â‰ˆ-refl (ğ’ŸP.pair-natural _ _ _) âŸ©
        mul ğ’Ÿ.âˆ˜ (ğ’ŸP.pair (F.fmor ğ’P.pâ‚) (F.fmor ğ’P.pâ‚‚) ğ’Ÿ.âˆ˜ F.fmor (ğ’P.pair fâ‚ fâ‚‚))
      â‰¡âŸ¨âŸ©
        mul ğ’Ÿ.âˆ˜ (mulâ»Â¹ ğ’Ÿ.âˆ˜ F.fmor (ğ’P.pair fâ‚ fâ‚‚))
      â‰ˆË˜âŸ¨ ğ’Ÿ.assoc _ _ _ âŸ©
        (mul ğ’Ÿ.âˆ˜ mulâ»Â¹) ğ’Ÿ.âˆ˜ F.fmor (ğ’P.pair fâ‚ fâ‚‚)
      â‰ˆâŸ¨ ğ’Ÿ.âˆ˜-cong (Fp .ğ’Ÿ.IsIso.inverseâˆ˜fâ‰ˆid) ğ’Ÿ.â‰ˆ-refl âŸ©
        ğ’Ÿ.id _ ğ’Ÿ.âˆ˜ F.fmor (ğ’P.pair fâ‚ fâ‚‚)
      â‰ˆâŸ¨ ğ’Ÿ.id-left âŸ©
        F.fmor (ğ’P.pair fâ‚ fâ‚‚)
      âˆ
      where open â‰ˆ-Reasoning ğ’Ÿ.isEquiv
