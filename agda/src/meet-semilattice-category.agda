{-# OPTIONS --postfix-projections --prop --safe #-}

-- Category of meet semilattices and meet preserving morphisms, which
-- is complete and CMon-enriched.

module meet-semilattice-category where

open import Level using (suc; 0â„“)
open import prop using (projâ‚; projâ‚‚)
open import prop-setoid using (IsEquivalence; module â‰ˆ-Reasoning)
open import basics using (IsPreorder; IsTop; IsMeet)
open import preorder using (Preorder; _Ã—_)
open import meet-semilattice
  using ( MeetSemilattice
        ; Îµm; _+m_; +m-cong; +m-comm; +m-assoc; +m-lunit
        ; comp-bilinearâ‚; comp-bilinearâ‚‚; comp-bilinear-Îµâ‚; comp-bilinear-Îµâ‚‚; ğŸ™)
  renaming (_=>_ to _=>M_; _â‰ƒm_ to _â‰ƒM_; id to idM; _âˆ˜_ to _âˆ˜M_;
            _âŠ•_ to _âŠ•M_;
            â‰ƒm-isEquivalence to â‰ƒM-isEquivalence)
open import categories using (Category; HasProducts; HasTerminal)
open import functor using (IsLimit; Limit; HasLimits; Functor; NatTrans; â‰ƒ-NatTrans)
import two

record Obj : Set (suc 0â„“) where
  no-eta-equality
  field
    carrier : Preorder
    meets   : MeetSemilattice carrier
  open Preorder carrier public
  open MeetSemilattice meets public
open Obj

record _â‡’_ (X Y : Obj) : Set where
  no-eta-equality
  field
    *â†’* : X .meets =>M Y .meets
  open _=>M_ *â†’* public
  open preorder._=>_ func public
open _â‡’_

record _â‰ƒm_ {X Y : Obj} (f g : X â‡’ Y) : Prop where
  no-eta-equality
  field
    fâ‰ƒf : f .*â†’* â‰ƒM g .*â†’*
  open _â‰ƒM_ fâ‰ƒf public
  open preorder._â‰ƒm_ eqfunc public
open _â‰ƒm_

cat : Category (suc 0â„“) 0â„“ 0â„“
cat .Category.obj = Obj
cat .Category._â‡’_ = _â‡’_
cat .Category._â‰ˆ_ = _â‰ƒm_
cat .Category.isEquiv .IsEquivalence.refl .fâ‰ƒf = â‰ƒM-isEquivalence .IsEquivalence.refl
cat .Category.isEquiv .IsEquivalence.sym fâ‰ƒg .fâ‰ƒf = â‰ƒM-isEquivalence .IsEquivalence.sym (fâ‰ƒg .fâ‰ƒf)
cat .Category.isEquiv .IsEquivalence.trans fâ‰ƒg gâ‰ƒh .fâ‰ƒf =
  â‰ƒM-isEquivalence .IsEquivalence.trans (fâ‰ƒg .fâ‰ƒf) (gâ‰ƒh .fâ‰ƒf)
cat .Category.id X .*â†’* = idM
cat .Category._âˆ˜_ f g .*â†’* = f .*â†’* âˆ˜M g .*â†’*
cat .Category.âˆ˜-cong fâ‚â‰ƒfâ‚‚ gâ‚â‰ƒgâ‚‚ .fâ‰ƒf = meet-semilattice.âˆ˜-cong (fâ‚â‰ƒfâ‚‚ .fâ‰ƒf) (gâ‚â‰ƒgâ‚‚ .fâ‰ƒf)
cat .Category.id-left .fâ‰ƒf = meet-semilattice.id-left
cat .Category.id-right .fâ‰ƒf = meet-semilattice.id-right
cat .Category.assoc f g h .fâ‰ƒf = meet-semilattice.assoc (f .*â†’*) (g .*â†’*) (h .*â†’*)

------------------------------------------------------------------------------
-- CMon-enrichment

open import commutative-monoid using (CommutativeMonoid)
open import cmon-enriched using (CMonEnriched)

cmon-enriched : CMonEnriched cat
cmon-enriched .CMonEnriched.homCM x y .CommutativeMonoid.Îµ .*â†’* = Îµm
cmon-enriched .CMonEnriched.homCM x y .CommutativeMonoid._+_ f g .*â†’* = (f .*â†’*) +m (g .*â†’*)
cmon-enriched .CMonEnriched.homCM x y .CommutativeMonoid.+-cong fâ‚â‰ƒfâ‚‚ gâ‚â‰ƒgâ‚‚ .fâ‰ƒf = +m-cong (fâ‚â‰ƒfâ‚‚ .fâ‰ƒf) (gâ‚â‰ƒgâ‚‚ .fâ‰ƒf)
cmon-enriched .CMonEnriched.homCM x y .CommutativeMonoid.+-lunit .fâ‰ƒf = +m-lunit
cmon-enriched .CMonEnriched.homCM x y .CommutativeMonoid.+-assoc {f}{g}{h} .fâ‰ƒf = +m-assoc {f = f .*â†’*} {g .*â†’*} {h .*â†’*}
cmon-enriched .CMonEnriched.homCM x y .CommutativeMonoid.+-comm {f}{g} .fâ‰ƒf = +m-comm {f = f .*â†’*} {g .*â†’*}
cmon-enriched .CMonEnriched.comp-bilinearâ‚ fâ‚ fâ‚‚ g .fâ‰ƒf = comp-bilinearâ‚ (fâ‚ .*â†’*) (fâ‚‚ .*â†’*) (g .*â†’*)
cmon-enriched .CMonEnriched.comp-bilinearâ‚‚ f gâ‚ gâ‚‚ .fâ‰ƒf = comp-bilinearâ‚‚ (f .*â†’*) (gâ‚ .*â†’*) (gâ‚‚ .*â†’*)
cmon-enriched .CMonEnriched.comp-bilinear-Îµâ‚ f .fâ‰ƒf = comp-bilinear-Îµâ‚ (f .*â†’*)
cmon-enriched .CMonEnriched.comp-bilinear-Îµâ‚‚ f .fâ‰ƒf = comp-bilinear-Îµâ‚‚ (f .*â†’*)

------------------------------------------------------------------------------
-- Limits

module _ (ğ’® : Category 0â„“ 0â„“ 0â„“) where

  private
    module ğ’® = Category ğ’®

  open Functor
  open NatTrans
  open â‰ƒ-NatTrans
  open _â‰ƒM_
  open preorder._=>_
  open preorder._â‰ƒm_

  record Î -Carrier (D : Functor ğ’® cat) : Set where
    field
      Î -func    : (x : ğ’®.obj) â†’ D .fobj x .Carrier
      Î -natural : âˆ€ {xâ‚ xâ‚‚} (f : xâ‚ ğ’®.â‡’ xâ‚‚) â†’ _â‰ƒ_ (D .fobj xâ‚‚) (D .fmor f .fun (Î -func xâ‚)) (Î -func xâ‚‚)
  open Î -Carrier

  Î  : Functor ğ’® cat â†’ Obj
  Î  D .carrier .Preorder.Carrier = Î -Carrier D
  Î  D .carrier .Preorder._â‰¤_ Î±â‚ Î±â‚‚ = âˆ€ s â†’ D .fobj s ._â‰¤_ (Î±â‚ .Î -func s) (Î±â‚‚ .Î -func s)
  Î  D .carrier .Preorder.â‰¤-isPreorder .IsPreorder.refl s = D .fobj s .â‰¤-refl
  Î  D .carrier .Preorder.â‰¤-isPreorder .IsPreorder.trans Î±â‰¤Î² Î²â‰¤Î³ s = D .fobj s .â‰¤-trans (Î±â‰¤Î² s) (Î²â‰¤Î³ s)
  Î  D .meets .MeetSemilattice._âˆ§_ Î±â‚ Î±â‚‚ .Î -func s = D .fobj s ._âˆ§_ (Î±â‚ .Î -func s) (Î±â‚‚ .Î -func s)
  Î  D .meets .MeetSemilattice._âˆ§_ Î±â‚ Î±â‚‚ .Î -natural {sâ‚}{sâ‚‚} f = begin
      D .fmor f .fun (D .fobj sâ‚ ._âˆ§_ (Î±â‚ .Î -func sâ‚) (Î±â‚‚ .Î -func sâ‚))
    â‰ˆË˜âŸ¨ âˆ§-preserving-â‰ƒ (D .fmor f) âŸ©
      D .fobj sâ‚‚ ._âˆ§_ (D .fmor f .fun (Î±â‚ .Î -func sâ‚)) (D .fmor f .fun (Î±â‚‚ .Î -func sâ‚))
    â‰ˆâŸ¨ âˆ§-cong (D .fobj sâ‚‚) (Î±â‚ .Î -natural f) (Î±â‚‚ .Î -natural f) âŸ©
      D .fobj sâ‚‚ ._âˆ§_ (Î±â‚ .Î -func sâ‚‚) (Î±â‚‚ .Î -func sâ‚‚)
    âˆ where open â‰ˆ-Reasoning (isEquivalence (D .fobj sâ‚‚))
  Î  D .meets .MeetSemilattice.âŠ¤ .Î -func s = D .fobj s .âŠ¤
  Î  D .meets .MeetSemilattice.âŠ¤ .Î -natural {sâ‚}{sâ‚‚} f = â‰ƒ-sym (D .fobj sâ‚‚) (âŠ¤-preserving-â‰ƒ (D .fmor f))
  Î  D .meets .MeetSemilattice.âˆ§-isMeet .IsMeet.Ï€â‚ s = D .fobj s .âˆ§-isMeet .IsMeet.Ï€â‚
  Î  D .meets .MeetSemilattice.âˆ§-isMeet .IsMeet.Ï€â‚‚ s = D .fobj s .âˆ§-isMeet .IsMeet.Ï€â‚‚
  Î  D .meets .MeetSemilattice.âˆ§-isMeet .IsMeet.âŸ¨_,_âŸ© Î±â‰¤Î² Î±â‰¤Î³ s = D .fobj s .âˆ§-isMeet .IsMeet.âŸ¨_,_âŸ© (Î±â‰¤Î² s) (Î±â‰¤Î³ s)
  Î  D .meets .MeetSemilattice.âŠ¤-isTop .IsTop.â‰¤-top s = D .fobj s .âŠ¤-isTop .IsTop.â‰¤-top

  limits : HasLimits ğ’® cat
  limits D .Limit.apex = Î  D
  limits D .Limit.cone .transf x .*â†’* ._=>M_.func .fun Î± = Î± .Î -func x
  limits D .Limit.cone .transf x .*â†’* ._=>M_.func .mono Î±â‚â‰¤Î±â‚‚ = Î±â‚â‰¤Î±â‚‚ x
  limits D .Limit.cone .transf x .*â†’* ._=>M_.âˆ§-preserving = D .fobj x .â‰¤-refl
  limits D .Limit.cone .transf x .*â†’* ._=>M_.âŠ¤-preserving = D .fobj x .â‰¤-refl
  limits D .Limit.cone .natural {x} {y} f .fâ‰ƒf .eqfunc .eqfun Î± = Î± .Î -natural f
  limits D .Limit.isLimit .IsLimit.lambda X Î± .*â†’* ._=>M_.func .fun x .Î -func s = Î± .transf s .fun x
  limits D .Limit.isLimit .IsLimit.lambda X Î± .*â†’* ._=>M_.func .fun x .Î -natural f = Î± .natural f .eqfun x
  limits D .Limit.isLimit .IsLimit.lambda X Î± .*â†’* ._=>M_.func .mono xâ‚â‰¤xâ‚‚ s = Î± .transf s .mono xâ‚â‰¤xâ‚‚
  limits D .Limit.isLimit .IsLimit.lambda X Î± .*â†’* ._=>M_.âˆ§-preserving s = Î± .transf s .âˆ§-preserving
  limits D .Limit.isLimit .IsLimit.lambda X Î± .*â†’* ._=>M_.âŠ¤-preserving s = Î± .transf s .âŠ¤-preserving
  limits D .Limit.isLimit .IsLimit.lambda-cong Î±â‰ƒÎ² .fâ‰ƒf .eqfunc .eqfun x .projâ‚ s = Î±â‰ƒÎ² .transf-eq s .eqfun x .projâ‚
  limits D .Limit.isLimit .IsLimit.lambda-cong Î±â‰ƒÎ² .fâ‰ƒf .eqfunc .eqfun x .projâ‚‚ s = Î±â‰ƒÎ² .transf-eq s .eqfun x .projâ‚‚
  limits D .Limit.isLimit .IsLimit.lambda-eval {X} Î± .transf-eq s .fâ‰ƒf .eqfunc .eqfun x = D .fobj s .â‰ƒ-refl
  limits D .Limit.isLimit .IsLimit.lambda-ext {X} f .fâ‰ƒf .eqfunc .eqfun x .projâ‚ s = D .fobj s .â‰¤-refl
  limits D .Limit.isLimit .IsLimit.lambda-ext {X} f .fâ‰ƒf .eqfunc .eqfun x .projâ‚‚ s = D .fobj s .â‰¤-refl

-- Do products and terminal object directly to get a nicer representation
products : HasProducts cat
products .HasProducts.prod X Y .carrier = X .carrier Ã— Y .carrier
products .HasProducts.prod X Y .meets = X .meets âŠ•M Y .meets
products .HasProducts.pâ‚ .*â†’* = meet-semilattice.projectâ‚
products .HasProducts.pâ‚‚ .*â†’* = meet-semilattice.projectâ‚‚
products .HasProducts.pair f g .*â†’* = meet-semilattice.âŸ¨ (f .*â†’*) , (g .*â†’*) âŸ©
products .HasProducts.pair-cong eqâ‚ eqâ‚‚ .fâ‰ƒf = meet-semilattice.âŸ¨âŸ©-cong (eqâ‚ .fâ‰ƒf) (eqâ‚‚ .fâ‰ƒf)
products .HasProducts.pair-pâ‚ f g .fâ‰ƒf = meet-semilattice.pair-pâ‚ (f .*â†’*) (g .*â†’*)
products .HasProducts.pair-pâ‚‚ f g .fâ‰ƒf = meet-semilattice.pair-pâ‚‚ (f .*â†’*) (g .*â†’*)
products .HasProducts.pair-ext f .fâ‰ƒf = meet-semilattice.pair-ext (f .*â†’*)

terminal : HasTerminal cat
terminal .HasTerminal.witness = record { meets = ğŸ™ }
terminal .HasTerminal.is-terminal .categories.IsTerminal.to-terminal .*â†’* = meet-semilattice.terminal
terminal .HasTerminal.is-terminal .categories.IsTerminal.to-terminal-ext f .fâ‰ƒf =
  meet-semilattice.terminal-unique _ _ _

-- = limitsâ†’products cat (limits product-diagram.cat)
--   where open import product-diagram using (limitsâ†’products)

TWO : Obj
TWO .carrier .Preorder.Carrier = two.Two
TWO .carrier .Preorder._â‰¤_ = two._â‰¤_
TWO .carrier .Preorder.â‰¤-isPreorder = two.â‰¤-isPreorder
TWO .meets .MeetSemilattice._âˆ§_ = two._âŠ“_
TWO .meets .MeetSemilattice.âŠ¤ = two.I
TWO .meets .MeetSemilattice.âˆ§-isMeet = two.âŠ“-isMeet
TWO .meets .MeetSemilattice.âŠ¤-isTop .IsTop.â‰¤-top = two.I-top
