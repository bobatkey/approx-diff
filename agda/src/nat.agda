{-# OPTIONS --prop --postfix-projections --safe #-}

module nat where

open import Level using (0â„“)
open import Data.Product using (_,_)
open import Data.Sum using (_âŠ_; injâ‚; injâ‚‚)
open import prop
open import basics
open import prop-setoid using (module â‰ˆ-Reasoning; Setoid; âŠ—-setoid; ğŸ™)
  renaming (_â‡’_ to _â‡’s_)

------------------------------------------------------------------------------
-- Reuse the builtin definitions so we get fast implementations

open import Agda.Builtin.Nat
  renaming (Nat to â„•; suc to succ)
  using (_+_; _*_; zero)
  public

------------------------------------------------------------------------------
data _â‰¤_ : â„• â†’ â„• â†’ Prop where
  0â‰¤n : âˆ€ {n} â†’ zero â‰¤ n
  sâ‰¤s : âˆ€ {m n} â†’ m â‰¤ n â†’ succ m â‰¤ succ n

infix 4 _â‰¤_

succ-increasing : âˆ€ {x} â†’ x â‰¤ succ x
succ-increasing {zero}   = 0â‰¤n
succ-increasing {succ x} = sâ‰¤s succ-increasing

â‰¤-refl : âˆ€ {x} â†’ x â‰¤ x
â‰¤-refl {zero}   = 0â‰¤n
â‰¤-refl {succ x} = sâ‰¤s â‰¤-refl

â‰¤-trans : âˆ€ {x y z} â†’ x â‰¤ y â†’ y â‰¤ z â†’ x â‰¤ z
â‰¤-trans 0â‰¤n       yâ‰¤z       = 0â‰¤n
â‰¤-trans (sâ‰¤s xâ‰¤y) (sâ‰¤s yâ‰¤z) = sâ‰¤s (â‰¤-trans xâ‰¤y yâ‰¤z)

â‰¤-total : âˆ€ x y â†’ (x â‰¤ y) âˆ¨ (y â‰¤ x)
â‰¤-total zero y = injâ‚ 0â‰¤n
â‰¤-total (succ x) zero = injâ‚‚ 0â‰¤n
â‰¤-total (succ x) (succ y) with â‰¤-total x y
... | injâ‚ xâ‰¤y = injâ‚ (sâ‰¤s xâ‰¤y)
... | injâ‚‚ yâ‰¤x = injâ‚‚ (sâ‰¤s yâ‰¤x)

â‰¤-isPreorder : IsPreorder _â‰¤_
â‰¤-isPreorder .IsPreorder.refl = â‰¤-refl
â‰¤-isPreorder .IsPreorder.trans = â‰¤-trans

open IsPreorder â‰¤-isPreorder
  using (_â‰ƒ_; â‰ƒ-refl; â‰ƒ-sym; â‰ƒ-trans)
  renaming (isEquivalence to â‰ƒ-isEquivalence)
  public

------------------------------------------------------------------------------
-- Equality and setoids

â‰ƒ-zero : zero â‰ƒ zero
â‰ƒ-zero .projâ‚ = 0â‰¤n
â‰ƒ-zero .projâ‚‚ = 0â‰¤n

succ-cong : âˆ€ {x y} â†’ x â‰ƒ y â†’ succ x â‰ƒ succ y
succ-cong p .projâ‚ = sâ‰¤s (projâ‚ p)
succ-cong p .projâ‚‚ = sâ‰¤s (projâ‚‚ p)

succ-injective : âˆ€ {x y} â†’ succ x â‰ƒ succ y â†’ x â‰ƒ y
succ-injective (sâ‰¤s p , _) .projâ‚ = p
succ-injective (_ , sâ‰¤s p) .projâ‚‚ = p

â„•â‚› : Setoid 0â„“ 0â„“
â„•â‚› .Setoid.Carrier = â„•
â„•â‚› .Setoid._â‰ˆ_ = _â‰ƒ_
â„•â‚› .Setoid.isEquivalence = â‰ƒ-isEquivalence

------------------------------------------------------------------------------
-- Strictly less than

data _<_ : â„• â†’ â„• â†’ Set where
  n<s : âˆ€ {x}   â†’ x < succ x
  <s : âˆ€ {x y} â†’ x < y â†’ x < succ y

s<s : âˆ€ {x y} â†’ x < y â†’ succ x < succ y
s<s n<s      = n<s
s<s (<s x<y) = <s (s<s x<y)

-- <-trans : âˆ€ {m n o} â†’ m < n â†’ n < o â†’ m < o
-- <-trans 0<s       (s<s n<o) = 0<s
-- <-trans (s<s m<n) (s<s n<o) = s<s (<-trans m<n n<o)

------------------------------------------------------------------------------
-- Joins and Meets

_âŠ”_ : â„• â†’ â„• â†’ â„•
zero   âŠ” y      = y
succ x âŠ” zero   = succ x
succ x âŠ” succ y = succ (x âŠ” y)

upperâ‚ : âˆ€ {x y} â†’ x â‰¤ (x âŠ” y)
upperâ‚ {zero}   {y}      = 0â‰¤n
upperâ‚ {succ x} {zero}   = â‰¤-refl
upperâ‚ {succ x} {succ y} = sâ‰¤s upperâ‚

upperâ‚‚ : âˆ€ {x y} â†’ y â‰¤ (x âŠ” y)
upperâ‚‚ {zero}   {zero}   = 0â‰¤n
upperâ‚‚ {zero}   {succ y} = â‰¤-refl
upperâ‚‚ {succ x} {zero}   = 0â‰¤n
upperâ‚‚ {succ x} {succ y} = sâ‰¤s upperâ‚‚

âŠ”-least : âˆ€ {x y z} â†’ x â‰¤ z â†’ y â‰¤ z â†’ (x âŠ” y) â‰¤ z
âŠ”-least 0â‰¤n       yâ‰¤z       = yâ‰¤z
âŠ”-least (sâ‰¤s xâ‰¤z) 0â‰¤n       = sâ‰¤s xâ‰¤z
âŠ”-least (sâ‰¤s xâ‰¤z) (sâ‰¤s yâ‰¤z) = sâ‰¤s (âŠ”-least xâ‰¤z yâ‰¤z)

-- FIXME: also have _âŠ_ version of this
âŠ”-split : âˆ€ {x y z} â†’ z â‰¤ (x âŠ” y) â†’ (z â‰¤ x) âˆ¨ (z â‰¤ y)
âŠ”-split {x} {y} {zero} zâ‰¤xâŠ”y = injâ‚ 0â‰¤n
âŠ”-split {zero} {y} {succ z} zâ‰¤xâŠ”y = injâ‚‚ zâ‰¤xâŠ”y
âŠ”-split {succ x} {zero} {succ z} zâ‰¤xâŠ”y = injâ‚ zâ‰¤xâŠ”y
âŠ”-split {succ x} {succ y} {succ z} (sâ‰¤s zâ‰¤xâŠ”y) with âŠ”-split {x} {y} {z} zâ‰¤xâŠ”y
... | injâ‚ xâ‚ = injâ‚ (sâ‰¤s xâ‚)
... | injâ‚‚ xâ‚ = injâ‚‚ (sâ‰¤s xâ‚)

âŠ”-chooses : âˆ€ x y â†’ (x âŠ” y â‰¤ x) âˆ¨ (x âŠ” y â‰¤ y)
âŠ”-chooses zero y = injâ‚‚ â‰¤-refl
âŠ”-chooses (succ x) zero = injâ‚ â‰¤-refl
âŠ”-chooses (succ x) (succ y) with âŠ”-chooses x y
... | injâ‚ p = injâ‚ (sâ‰¤s p)
... | injâ‚‚ p = injâ‚‚ (sâ‰¤s p)

_âŠ“_ : â„• â†’ â„• â†’ â„•
zero   âŠ“ y      = zero
succ x âŠ“ zero   = zero
succ x âŠ“ succ y = succ (x âŠ“ y)

âŠ“-greatest : âˆ€ {x y z} â†’ z â‰¤ x â†’ z â‰¤ y â†’ z â‰¤ (x âŠ“ y)
âŠ“-greatest 0â‰¤n zâ‰¤y = 0â‰¤n
âŠ“-greatest (sâ‰¤s zâ‰¤x) (sâ‰¤s zâ‰¤y) = sâ‰¤s (âŠ“-greatest zâ‰¤x zâ‰¤y)

lowerâ‚ : âˆ€ {x y} â†’ (x âŠ“ y) â‰¤ x
lowerâ‚ {zero}   {y}      = 0â‰¤n
lowerâ‚ {succ x} {zero}   = 0â‰¤n
lowerâ‚ {succ x} {succ y} = sâ‰¤s lowerâ‚

lowerâ‚‚ : âˆ€ {x y} â†’ (x âŠ“ y) â‰¤ y
lowerâ‚‚ {zero}   {y}      = 0â‰¤n
lowerâ‚‚ {succ x} {zero}   = 0â‰¤n
lowerâ‚‚ {succ x} {succ y} = sâ‰¤s lowerâ‚‚

âŠ“-isMeet : IsMeet â‰¤-isPreorder _âŠ“_
âŠ“-isMeet .IsMeet.Ï€â‚ = lowerâ‚
âŠ“-isMeet .IsMeet.Ï€â‚‚ = lowerâ‚‚
âŠ“-isMeet .IsMeet.âŸ¨_,_âŸ© = âŠ“-greatest

open IsMeet âŠ“-isMeet
  using ()
  renaming (mono to âŠ“-mono; cong to âŠ“-cong; assoc to âŠ“-assoc; idem to âŠ“-idem)

âŠ“-chooses : âˆ€ x y â†’ (x â‰¤ x âŠ“ y) âˆ¨ (y â‰¤ x âŠ“ y)
âŠ“-chooses zero     y    = injâ‚ 0â‰¤n
âŠ“-chooses (succ x) zero = injâ‚‚ 0â‰¤n
âŠ“-chooses (succ x) (succ y) with âŠ“-chooses x y
... | injâ‚ p = injâ‚ (sâ‰¤s p)
... | injâ‚‚ p = injâ‚‚ (sâ‰¤s p)

-- Distributivity: FIXME: follows from totality and monotonicity

âŠ“-âŠ”-distrib : âˆ€ x y z â†’ x âŠ“ (y âŠ” z) â‰¤ (x âŠ“ y) âŠ” (x âŠ“ z)
âŠ“-âŠ”-distrib zero     y        z        = â‰¤-refl
âŠ“-âŠ”-distrib (succ x) zero     z        = â‰¤-refl
âŠ“-âŠ”-distrib (succ x) (succ y) zero     = â‰¤-refl
âŠ“-âŠ”-distrib (succ x) (succ y) (succ z) = sâ‰¤s (âŠ“-âŠ”-distrib x y z)

âŠ”-âŠ“-distrib : âˆ€ x y z â†’ (x âŠ” y) âŠ“ (x âŠ” z) â‰¤ x âŠ” (y âŠ“ z)
âŠ”-âŠ“-distrib zero     y        z        = â‰¤-refl
âŠ”-âŠ“-distrib (succ x) zero     zero     = sâ‰¤s lowerâ‚
âŠ”-âŠ“-distrib (succ x) zero     (succ z) = sâ‰¤s lowerâ‚
âŠ”-âŠ“-distrib (succ x) (succ y) zero     = sâ‰¤s lowerâ‚‚
âŠ”-âŠ“-distrib (succ x) (succ y) (succ z) = sâ‰¤s (âŠ”-âŠ“-distrib x y z)

------------------------------------------------------------------------------
-- Addition

+-increasing : âˆ€ {x y} â†’ y â‰¤ (x + y)
+-increasing {zero} = â‰¤-refl
+-increasing {succ x} = â‰¤-trans succ-increasing (sâ‰¤s (+-increasing {x}))

+-mono : âˆ€ {xâ‚ xâ‚‚ yâ‚ yâ‚‚} â†’ xâ‚ â‰¤ xâ‚‚ â†’ yâ‚ â‰¤ yâ‚‚ â†’ (xâ‚ + yâ‚) â‰¤ (xâ‚‚ + yâ‚‚)
+-mono 0â‰¤n     0â‰¤n     = 0â‰¤n
+-mono 0â‰¤n     (sâ‰¤s q) = â‰¤-trans (sâ‰¤s q) +-increasing
+-mono (sâ‰¤s p) q       = sâ‰¤s (+-mono p q)

+-lunit : âˆ€ {x} â†’ (zero + x) â‰ƒ x
+-lunit = â‰ƒ-refl

+-runit : âˆ€ {x} â†’ (x + zero) â‰ƒ x
+-runit {zero}   = â‰ƒ-zero
+-runit {succ x} = succ-cong +-runit

+-assoc : âˆ€ {x y z} â†’ ((x + y) + z) â‰ƒ (x + (y + z))
+-assoc {zero}   = â‰ƒ-refl
+-assoc {succ x} = succ-cong (+-assoc {x})

+-isMonoid : IsMonoid â‰¤-isPreorder _+_ zero
+-isMonoid .IsMonoid.mono = +-mono
+-isMonoid .IsMonoid.assoc {x} {y} {z} = +-assoc {x} {y} {z}
+-isMonoid .IsMonoid.lunit = +-lunit
+-isMonoid .IsMonoid.runit = +-runit

open IsMonoid +-isMonoid
  using ()
  renaming (cong to +-cong; interchange to +-interchange)

+-succ : âˆ€ {x y} â†’ (x + succ y) â‰ƒ succ (x + y)
+-succ {zero}   = succ-cong â‰ƒ-refl
+-succ {succ x} = succ-cong +-succ

+-comm : âˆ€ {x y} â†’ (x + y) â‰ƒ (y + x)
+-comm {zero}   = â‰ƒ-sym +-runit
+-comm {succ x} = â‰ƒ-trans (succ-cong (+-comm {x})) (â‰ƒ-sym +-succ)

+-cancelâ‚— : âˆ€ {x y z} â†’ (x + y) â‰¤ (x + z) â†’ y â‰¤ z
+-cancelâ‚— {zero}   p       = p
+-cancelâ‚— {succ x} (sâ‰¤s p) = +-cancelâ‚— p

+-canceláµ£ : âˆ€ {x y z} â†’ (y + x) â‰¤ (z + x) â†’ y â‰¤ z
+-canceláµ£ {x}{y}{z} p =
  +-cancelâ‚— (â‰¤-trans (+-comm {x} {y} .projâ‚) (â‰¤-trans p (+-comm {x} {z} .projâ‚‚)))

-- Follows from totality and increasingness
âŠ“â‰¤+ : âˆ€ {x y} â†’ x âŠ“ y â‰¤ x + y
âŠ“â‰¤+ {zero}   {y}      = 0â‰¤n
âŠ“â‰¤+ {succ x} {zero}   = 0â‰¤n
âŠ“â‰¤+ {succ x} {succ y} = sâ‰¤s (â‰¤-trans âŠ“â‰¤+ (+-mono {x} â‰¤-refl succ-increasing))

-- Distributivity with _âŠ”_ and _âŠ“_, which follows from monotonicity of
-- _+_ and totality of the order. FIXME: abstract this, so it works
-- for all other distributivity properties.

+-âŠ“-distribâ‚— : âˆ€ x y z â†’ (x + y) âŠ“ (x + z) â‰¤ x + (y âŠ“ z)
+-âŠ“-distribâ‚— x y z with â‰¤-total y z
... | injâ‚ p = â‰¤-trans lowerâ‚ (+-mono {x} â‰¤-refl (âŠ“-greatest â‰¤-refl p))
... | injâ‚‚ p = â‰¤-trans lowerâ‚‚ (+-mono {x} â‰¤-refl (âŠ“-greatest p â‰¤-refl))

+-âŠ“-distrib : âˆ€ x y z â†’ x + (y âŠ“ z) â‰¤ (x + y) âŠ“ (x + z)
+-âŠ“-distrib x y z = âŠ“-greatest (+-mono {x} â‰¤-refl lowerâ‚) (+-mono {x} â‰¤-refl lowerâ‚‚)

+-âŠ”-distrib : âˆ€ x y z â†’ x + (y âŠ” z) â‰¤ (x + y) âŠ” (x + z)
+-âŠ”-distrib x y z with â‰¤-total y z
... | injâ‚ p = â‰¤-trans (+-mono {x} â‰¤-refl (âŠ”-least p â‰¤-refl)) upperâ‚‚
... | injâ‚‚ p = â‰¤-trans (+-mono {x} â‰¤-refl (âŠ”-least â‰¤-refl p)) upperâ‚

------------------------------------------------------------------------------
-- Monus (a residual for + on â„•^op)
_âˆ¸_ : â„• â†’ â„• â†’ â„•
x      âˆ¸ zero   = x
zero   âˆ¸ succ y = zero
succ x âˆ¸ succ y = x âˆ¸ y

eval : âˆ€ {x y} â†’ y â‰¤ (x + (y âˆ¸ x))
eval {zero}   {y}      = â‰¤-refl
eval {succ x} {zero}   = 0â‰¤n
eval {succ x} {succ y} = sâ‰¤s (eval {x} {y})

lambda : âˆ€ {x y z} â†’ x â‰¤ (y + z) â†’ (x âˆ¸ y) â‰¤ z
lambda {x}      {zero}   f       = f
lambda {zero}   {succ y} 0â‰¤n     = 0â‰¤n
lambda {succ x} {succ y} (sâ‰¤s f) = lambda f

-- Totality means that this is an op-pre-total order
pre-total : âˆ€ x y â†’ (x âˆ¸ y) âŠ“ (y âˆ¸ x) â‰¤ zero
pre-total x y with â‰¤-total x y
... | injâ‚ xâ‰¤y = â‰¤-trans lowerâ‚ (lambda (â‰¤-trans xâ‰¤y (+-runit .projâ‚‚)))
... | injâ‚‚ yâ‰¤x = â‰¤-trans lowerâ‚‚ (lambda (â‰¤-trans yâ‰¤x (+-runit .projâ‚‚)))

---------------------------------------------------------------------------------------
-- FIXME: probably worth making a helper function for
-- nullary/unary/binary operations on setoids.
module _ where

  open _â‡’s_

  add : âŠ—-setoid â„•â‚› â„•â‚› â‡’s â„•â‚›
  add .func (x , y) = x + y
  add .func-resp-â‰ˆ (xâ‚â‰ˆxâ‚‚ , yâ‚â‰ˆyâ‚‚) = +-cong xâ‚â‰ˆxâ‚‚ yâ‚â‰ˆyâ‚‚

  zero-m : ğŸ™ {0â„“} {0â„“} â‡’s â„•â‚›
  zero-m .func x = zero
  zero-m .func-resp-â‰ˆ x = â‰ƒ-refl

------------------------------------------------------------------------------
-- Multiplication: _*_ is defined in Agda.Builtin.Nat

*-zero : âˆ€ {x} â†’ (x * zero) â‰ƒ zero
*-zero {zero}   = â‰ƒ-refl
*-zero {succ x} = *-zero {x}

*-succ : âˆ€ {x y} â†’ (y * succ x) â‰ƒ (y + (y * x))
*-succ {x} {zero} = â‰ƒ-refl
*-succ {x} {succ y} =
  begin succ (x + (y * succ x))  â‰ˆâŸ¨ succ-cong (+-cong â‰ƒ-refl (*-succ {x} {y})) âŸ©
        succ (x + (y + (y * x))) â‰ˆË˜âŸ¨ succ-cong (+-assoc {x} {y}) âŸ©
        succ ((x + y) + (y * x)) â‰ˆâŸ¨ succ-cong (+-cong (+-comm {x} {y}) â‰ƒ-refl) âŸ©
        succ ((y + x) + (y * x)) â‰ˆâŸ¨ succ-cong (+-assoc {y} {x}) âŸ©
        succ (y + (x + (y * x))) âˆ
  where open â‰ˆ-Reasoning â‰ƒ-isEquivalence

*-mono : âˆ€ {xâ‚ xâ‚‚ yâ‚ yâ‚‚} â†’ xâ‚ â‰¤ xâ‚‚ â†’ yâ‚ â‰¤ yâ‚‚ â†’ (xâ‚ * yâ‚) â‰¤ (xâ‚‚ * yâ‚‚)
*-mono 0â‰¤n         yâ‚â‰¤yâ‚‚ = 0â‰¤n
*-mono (sâ‰¤s xâ‚â‰¤xâ‚‚) yâ‚â‰¤yâ‚‚ = +-mono yâ‚â‰¤yâ‚‚ (*-mono xâ‚â‰¤xâ‚‚ yâ‚â‰¤yâ‚‚)

*-lunit : âˆ€ {x} â†’ (1 * x) â‰ƒ x
*-lunit = +-runit

*-runit : âˆ€ {x} â†’ (x * 1) â‰ƒ x
*-runit {zero}   = â‰ƒ-zero
*-runit {succ x} = succ-cong *-runit

*-comm : âˆ€ {x y} â†’ (x * y) â‰ƒ (y * x)
*-comm {zero}   {y} = â‰ƒ-sym (*-zero {y})
*-comm {succ x} {y} = â‰ƒ-trans (+-cong â‰ƒ-refl (*-comm {x} {y})) (â‰ƒ-sym (*-succ {x} {y}))

0-*-distribâ‚— : âˆ€ {x} â†’ x * 0 â‰ƒ 0
0-*-distribâ‚— {zero} = â‰ƒ-refl
0-*-distribâ‚— {succ x} = 0-*-distribâ‚— {x}

0-*-distribáµ£ : âˆ€ {x} â†’ 0 * x â‰ƒ 0
0-*-distribáµ£ {x} = â‰ƒ-refl

+-*-distribâ‚— : âˆ€ {x y z} â†’ (x * (y + z)) â‰ƒ ((x * y) + (x * z))
+-*-distribâ‚— {zero} = â‰ƒ-refl
+-*-distribâ‚— {succ x} {y} {z} =
  begin
    ((y + z) + (x * (y + z)))       â‰ˆâŸ¨ +-cong â‰ƒ-refl (+-*-distribâ‚— {x} {y} {z}) âŸ©
    ((y + z) + ((x * y) + (x * z))) â‰ˆâŸ¨ +-interchange (Î» {x} {y} â†’ +-comm {x} {y} .projâ‚) {y} {z} âŸ©
    ((y + (x * y)) + (z + (x * z))) âˆ
  where open â‰ˆ-Reasoning â‰ƒ-isEquivalence

+-*-distribáµ£ : âˆ€ {x y z} â†’ ((y + z) * x) â‰ƒ ((y * x) + (z * x))
+-*-distribáµ£ {x} {y} {z} =
  begin
    (y + z) * x       â‰ˆâŸ¨ *-comm {y + z} {x} âŸ©
    x * (y + z)       â‰ˆâŸ¨ +-*-distribâ‚— {x} {y} {z} âŸ©
    (x * y) + (x * z) â‰ˆâŸ¨ +-cong (*-comm {x} {y}) (*-comm {x} {z}) âŸ©
    (y * x) + (z * x) âˆ
  where open â‰ˆ-Reasoning â‰ƒ-isEquivalence

*-assoc : âˆ€ {x y z} â†’ ((x * y) * z) â‰ƒ (x * (y * z))
*-assoc {zero} = â‰ƒ-refl
*-assoc {succ x} {y} {z} =
  begin
    (y + (x * y)) * z       â‰ˆâŸ¨ +-*-distribáµ£ {z} {y} {x * y} âŸ©
    (y * z) + ((x * y) * z) â‰ˆâŸ¨ +-cong â‰ƒ-refl (*-assoc {x} {y} {z}) âŸ©
    (y * z) + (x * (y * z)) âˆ
  where open â‰ˆ-Reasoning â‰ƒ-isEquivalence

*-isMonoid : IsMonoid â‰¤-isPreorder _*_ 1
*-isMonoid .IsMonoid.mono = *-mono
*-isMonoid .IsMonoid.assoc {x} {y} {z} = *-assoc {x} {y} {z}
*-isMonoid .IsMonoid.lunit = *-lunit
*-isMonoid .IsMonoid.runit = *-runit

-- â€œfeedback with an initial stateâ€
*-canceláµ£ : âˆ€ {x y z} â†’ 1 â‰¤ x â†’ (y * x) â‰¤ (z * x) â†’ y â‰¤ z
*-canceláµ£ {succ x} {zero}   {z}      (sâ‰¤s p) 0â‰¤n     = 0â‰¤n
*-canceláµ£ {succ x} {succ y} {succ z} (sâ‰¤s p) (sâ‰¤s q) = sâ‰¤s (*-canceláµ£ (sâ‰¤s p) (+-cancelâ‚— q))

*-cancelâ‚— : âˆ€ {x y z} â†’ 1 â‰¤ x â†’ (x * y) â‰¤ (x * z) â†’ y â‰¤ z
*-cancelâ‚— {x} {y} {z} oneâ‰¤x xyâ‰¤xz =
  *-canceláµ£ oneâ‰¤x (begin y * x â‰¤âŸ¨ *-comm {y} .projâ‚ âŸ©
                         x * y â‰¤âŸ¨ xyâ‰¤xz âŸ©
                         x * z â‰¤âŸ¨ *-comm {x} .projâ‚ âŸ©
                         z * x âˆ)
  where open â‰¤-Reasoning â‰¤-isPreorder

-- FIXME: _+_ and _*_ form a semiring

------------------------------------------------------------------------------
-- Even/odd

mutual
  data Even : â„• â†’ Set where
    zero : Even 0
    succ : âˆ€ {n} â†’ Odd n â†’ Even (succ n)

  data Odd : â„• â†’ Set where
    succ : âˆ€ {n} â†’ Even n â†’ Odd (succ n)

evenâŠodd : âˆ€ n â†’ Even n âŠ Odd n
evenâŠodd zero = injâ‚ zero
evenâŠodd (succ n) with evenâŠodd n
... | injâ‚ x = injâ‚‚ (succ x)
... | injâ‚‚ y = injâ‚ (succ y)

------------------------------------------------------------------------------
-- Halving

-- Floor of n/2
âŒŠ_/2âŒ‹ : â„• â†’ â„•
âŒŠ zero          /2âŒ‹ = 0
âŒŠ succ zero     /2âŒ‹ = 0
âŒŠ succ (succ n) /2âŒ‹ = succ âŒŠ n /2âŒ‹

-- Ceiling of n/2
âŒˆ_/2âŒ‰ : â„• â†’ â„•
âŒˆ zero          /2âŒ‰ = 0
âŒˆ succ zero     /2âŒ‰ = 1
âŒˆ succ (succ n) /2âŒ‰ = succ âŒˆ n /2âŒ‰

even-agree : âˆ€ n â†’ Even n â†’ âŒŠ n /2âŒ‹ â‰ƒ âŒˆ n /2âŒ‰
even-agree zero            zero            = â‰ƒ-refl
even-agree (succ (succ n)) (succ (succ e)) = succ-cong (even-agree n e)

odd-off-by-one : âˆ€ n â†’ Odd n â†’ succ âŒŠ n /2âŒ‹ â‰ƒ âŒˆ n /2âŒ‰
odd-off-by-one (succ zero)     (succ zero)     = â‰ƒ-refl
odd-off-by-one (succ (succ n)) (succ (succ o)) = succ-cong (odd-off-by-one n o)

even-âŒŠ/2âŒ‹+âŒŠ/2âŒ‹ : âˆ€ n â†’ Even n â†’ (âŒŠ n  /2âŒ‹ + âŒŠ n  /2âŒ‹) â‰ƒ n
even-âŒŠ/2âŒ‹+âŒŠ/2âŒ‹ zero            zero            = â‰ƒ-refl
even-âŒŠ/2âŒ‹+âŒŠ/2âŒ‹ (succ (succ n)) (succ (succ e)) = â‰ƒ-trans (succ-cong +-succ) (succ-cong (succ-cong (even-âŒŠ/2âŒ‹+âŒŠ/2âŒ‹ n e)))

even-âŒˆ/2âŒ‰+âŒˆ/2âŒ‰ : âˆ€ n â†’ Even n â†’ (âŒˆ n  /2âŒ‰ + âŒˆ n  /2âŒ‰) â‰ƒ n
even-âŒˆ/2âŒ‰+âŒˆ/2âŒ‰ .0 zero = â‰ƒ-refl
even-âŒˆ/2âŒ‰+âŒˆ/2âŒ‰ .(succ (succ _)) (succ (succ x)) = â‰ƒ-trans (succ-cong +-succ) (succ-cong (succ-cong (even-âŒˆ/2âŒ‰+âŒˆ/2âŒ‰ _ x)))

odd-âŒŠ/2âŒ‹+âŒŠ/2âŒ‹ : âˆ€ n â†’ Odd n â†’ succ (âŒŠ n  /2âŒ‹ + âŒŠ n  /2âŒ‹) â‰ƒ n
odd-âŒŠ/2âŒ‹+âŒŠ/2âŒ‹ 1               (succ zero)     = â‰ƒ-refl
odd-âŒŠ/2âŒ‹+âŒŠ/2âŒ‹ (succ (succ _)) (succ (succ x)) = succ-cong (succ-cong (â‰ƒ-trans +-succ (odd-âŒŠ/2âŒ‹+âŒŠ/2âŒ‹ _ x)))

odd-âŒˆ/2âŒ‰+âŒˆ/2âŒ‰ : âˆ€ n â†’ Odd n â†’ (âŒˆ n /2âŒ‰ + âŒˆ n /2âŒ‰) â‰ƒ succ n
odd-âŒˆ/2âŒ‰+âŒˆ/2âŒ‰ .1               (succ zero)     = â‰ƒ-refl
odd-âŒˆ/2âŒ‰+âŒˆ/2âŒ‰ .(succ (succ _)) (succ (succ x)) = succ-cong (â‰ƒ-trans +-succ (succ-cong (odd-âŒˆ/2âŒ‰+âŒˆ/2âŒ‰ _ x)))

/2-< : âˆ€ {n} â†’ âŒŠ n /2âŒ‹ < succ n
/2-< {zero}          = n<s
/2-< {succ zero}     = <s n<s
/2-< {succ (succ n)} = <s (s<s /2-<)

/2-<' : âˆ€ {n} â†’ âŒˆ n /2âŒ‰ < succ n
/2-<' {zero}          = n<s
/2-<' {succ zero}     = n<s
/2-<' {succ (succ n)} = <s (s<s /2-<')

------------------------------------------------------------------------------
-- Exponentiation by 2
2^ : â„• â†’ â„•
2^ zero     = 1
2^ (succ n) = 2 * 2^ n

------------------------------------------------------------------------------
-- Logarithm, using complete induction and repeated halving.

data Acc (n : â„•) : Set where
  acc : (âˆ€ {m} â†’ m < n â†’ Acc m) â†’ Acc n

mutual
  <-acc : âˆ€ {n} â†’ Acc n
  <-acc = acc <-acc-helper

  <-acc-helper : âˆ€ {m n} â†’ m < n â†’ Acc m
  <-acc-helper {m} {succ m} n<s = <-acc
  <-acc-helper {m} {succ _} (<s m<n) = <-acc-helper m<n

âŒŠlog2'âŒ‹ : (n : â„•) â†’ Acc n â†’ â„•
âŒŠlog2'âŒ‹ zero            _       = zero
âŒŠlog2'âŒ‹ (succ zero)     _       = zero
âŒŠlog2'âŒ‹ (succ (succ n)) (acc r) = succ (âŒŠlog2'âŒ‹ (succ âŒŠ n /2âŒ‹) (r (s<s /2-<)))

âŒŠlog2âŒ‹ : â„• â†’ â„•
âŒŠlog2âŒ‹ n = âŒŠlog2'âŒ‹ n <-acc

-- should have 2^ (âŒŠlog2âŒ‹ n) â‰¤ n â‰¤ 2^ (âŒˆlog2âŒ‰ n)
-- and they are at most one apart
âŒˆlog2'âŒ‰ : (n : â„•) â†’ Acc n â†’ â„•
âŒˆlog2'âŒ‰ zero            _       = zero
âŒˆlog2'âŒ‰ (succ zero)     _       = zero
âŒˆlog2'âŒ‰ (succ (succ n)) (acc r) = succ (âŒˆlog2'âŒ‰ (succ âŒˆ n /2âŒ‰) (r (s<s /2-<')))

âŒˆlog2âŒ‰ : â„• â†’ â„•
âŒˆlog2âŒ‰ n = âŒˆlog2'âŒ‰ n <-acc

neq-0 : â„• â†’ Prop
neq-0 zero     = âŠ¥
neq-0 (succ _) = âŠ¤

n+nâ‰ƒ2*n : âˆ€ n â†’ n + n â‰ƒ 2 * n
n+nâ‰ƒ2*n n = +-cong {n} â‰ƒ-refl (â‰ƒ-sym +-runit)

-- FIXME: need succ âŒŠ n/2âŒ‹ + succ âŒŠ n/2âŒ‹ â‰¤ succ (succ n)

-- FIXME: move this up to the halving section
ceil-lemma : âˆ€ n â†’ succ (succ n) â‰¤ succ âŒˆ n /2âŒ‰ + succ âŒˆ n /2âŒ‰
ceil-lemma zero = â‰¤-refl
ceil-lemma (succ zero) = sâ‰¤s (sâ‰¤s (sâ‰¤s 0â‰¤n))
ceil-lemma (succ (succ n)) = sâ‰¤s (sâ‰¤s (â‰¤-trans (ceil-lemma n) (+-succ .projâ‚‚)))

âŒˆlog2'âŒ‰-upper : (n : â„•) (r : Acc n) â†’ n â‰¤ 2^ (âŒˆlog2'âŒ‰ n r)
âŒˆlog2'âŒ‰-upper zero            r = 0â‰¤n
âŒˆlog2'âŒ‰-upper (succ zero)     r = â‰¤-refl
âŒˆlog2'âŒ‰-upper (succ (succ n)) (acc r) = begin
    succ (succ n)
  â‰¤âŸ¨ ceil-lemma n âŸ©
    succ âŒˆ n /2âŒ‰ + succ âŒˆ n /2âŒ‰
  â‰¤âŸ¨ +-mono p p âŸ©
    2^ (âŒˆlog2'âŒ‰ (succ âŒˆ n /2âŒ‰) (r (s<s /2-<'))) + 2^ (âŒˆlog2'âŒ‰ (succ âŒˆ n /2âŒ‰) (r (s<s /2-<')))
  â‰¤âŸ¨ n+nâ‰ƒ2*n (2^ (âŒˆlog2'âŒ‰ (succ âŒˆ n /2âŒ‰) (r (s<s /2-<')))) .projâ‚ âŸ©
    2 * 2^ (âŒˆlog2'âŒ‰ (succ âŒˆ n /2âŒ‰) (r (s<s /2-<')))
  âˆ
  where p : succ âŒˆ n /2âŒ‰ â‰¤ 2^ (âŒˆlog2'âŒ‰ (succ âŒˆ n /2âŒ‰) (r (s<s /2-<')))
        p = âŒˆlog2'âŒ‰-upper (succ âŒˆ n /2âŒ‰) (r (s<s /2-<'))
        open â‰¤-Reasoning â‰¤-isPreorder

âŒˆlog2âŒ‰-upper : âˆ€ n â†’ n â‰¤ 2^ (âŒˆlog2âŒ‰ n)
âŒˆlog2âŒ‰-upper n = âŒˆlog2'âŒ‰-upper n <-acc


-- FIXME: âŒŠlog2'âŒ‹-lower (except for 0), and they are always within 1
-- of each other
