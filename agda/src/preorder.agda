{-# OPTIONS --postfix-projections --safe --prop #-}

module preorder where

open import Level
open import prop
open import Data.Unit using (tt)
open import Data.Product using (_,_)
open import basics
open import prop-setoid using (IsEquivalence)

record Preorder : Set (suc 0â„“) where
  no-eta-equality
  field
    Carrier      : Set
    _â‰¤_          : Carrier â†’ Carrier â†’ Prop
    â‰¤-isPreorder : IsPreorder _â‰¤_

  open IsPreorder â‰¤-isPreorder
    renaming (refl to â‰¤-refl; trans to â‰¤-trans)
    using (isEquivalence; _â‰ƒ_) public
  open IsEquivalence isEquivalence
    renaming (refl to â‰ƒ-refl; sym to â‰ƒ-sym; trans to â‰ƒ-trans) public

module _ where
  open Preorder

  -- Monotone functions
  record _=>_ (A B : Preorder) : Set where
    open Preorder
    private
      module A = Preorder A
      module B = Preorder B
    field
      fun : A .Carrier â†’ B .Carrier
      mono : âˆ€ {xâ‚} {xâ‚‚} â†’ A ._â‰¤_ xâ‚ xâ‚‚ â†’ B ._â‰¤_ (fun xâ‚) (fun xâ‚‚)

    resp-â‰ƒ : âˆ€ {xâ‚ xâ‚‚} â†’ xâ‚ A.â‰ƒ xâ‚‚ â†’ fun xâ‚ B.â‰ƒ fun xâ‚‚
    resp-â‰ƒ xâ‚â‰ƒxâ‚‚ .projâ‚ = mono (xâ‚â‰ƒxâ‚‚ .projâ‚)
    resp-â‰ƒ xâ‚â‰ƒxâ‚‚ .projâ‚‚ = mono (xâ‚â‰ƒxâ‚‚ .projâ‚‚)

module _ {A B : Preorder} where
  open _=>_
  private
    module A = Preorder A
    module B = Preorder B

  record _â‰ƒm_ (f g : A => B) : Prop where
    field
      eqfun : âˆ€ x â†’ f .fun x B.â‰ƒ g .fun x

  open IsEquivalence
  open _â‰ƒm_

  â‰ƒm-isEquivalence : IsEquivalence (_â‰ƒm_)
  â‰ƒm-isEquivalence .refl .eqfun x = B.â‰ƒ-refl
  â‰ƒm-isEquivalence .sym e .eqfun x = B.â‰ƒ-sym (e .eqfun x)
  â‰ƒm-isEquivalence .trans eâ‚ eâ‚‚ .eqfun x = B.â‰ƒ-trans (eâ‚ .eqfun x) (eâ‚‚ .eqfun x)

module _ where
  open Preorder
  open _=>_

  id : âˆ€ {A : Preorder} â†’ A => A
  id .fun x = x
  id .mono xâ‚â‰¤xâ‚‚ = xâ‚â‰¤xâ‚‚

  _âˆ˜_ : âˆ€ {A B C : Preorder} â†’ B => C â†’ A => B â†’ A => C
  (f âˆ˜ g) .fun x = f .fun (g .fun x)
  (f âˆ˜ g) .mono xâ‚â‰¤xâ‚‚ = f .mono (g .mono xâ‚â‰¤xâ‚‚)

  open _â‰ƒm_

  âˆ˜-cong : âˆ€ {A B C : Preorder} {fâ‚ fâ‚‚ : B => C} {gâ‚ gâ‚‚ : A => B} â†’ fâ‚ â‰ƒm fâ‚‚ â†’ gâ‚ â‰ƒm gâ‚‚ â†’ (fâ‚ âˆ˜ gâ‚) â‰ƒm (fâ‚‚ âˆ˜ gâ‚‚)
  âˆ˜-cong {A}{B}{C} {fâ‚ = fâ‚} fâ‚â‰ƒfâ‚‚ gâ‚â‰ƒgâ‚‚ .eqfun x =
    C .â‰ƒ-trans (resp-â‰ƒ fâ‚ (gâ‚â‰ƒgâ‚‚ .eqfun x)) (fâ‚â‰ƒfâ‚‚ .eqfun _)

  id-left : âˆ€ {A B : Preorder} â†’ {f : A => B} â†’ (id âˆ˜ f) â‰ƒm f
  id-left {A} {B} .eqfun x = B .â‰ƒ-refl

  id-right : âˆ€ {A B : Preorder} â†’ {f : A => B} â†’ (f âˆ˜ id) â‰ƒm f
  id-right {A} {B} .eqfun x = B .â‰ƒ-refl

  assoc : âˆ€ {A B C D : Preorder} (f : C => D) (g : B => C) (h : A => B) â†’ ((f âˆ˜ g) âˆ˜ h) â‰ƒm (f âˆ˜ (g âˆ˜ h))
  assoc {D = D} f g h .eqfun x = D .â‰ƒ-refl

module _ where
  open Preorder

  -- Unit preorder
  ğŸ™ : Preorder
  ğŸ™ .Carrier = Data.Unit.âŠ¤
  ğŸ™ ._â‰¤_ tt tt = âŠ¤
  ğŸ™ .â‰¤-isPreorder .IsPreorder.refl = tt
  ğŸ™ .â‰¤-isPreorder .IsPreorder.trans tt tt = tt

-- Lifting
data LCarrier (X : Set) : Set where
  bottom : LCarrier X
  <_>    : X â†’ LCarrier X

module _ {X : Set} {_â‰¤_ : X â†’ X â†’ Prop} (â‰¤-isPreorder : IsPreorder _â‰¤_) where

  _â‰¤L_ : LCarrier X â†’ LCarrier X â†’ Prop
  bottom â‰¤L _     = âŠ¤
  < x > â‰¤L bottom = âŠ¥
  < x > â‰¤L < x' > = x â‰¤ x'

  open IsPreorder

  â‰¤L-isPreorder : IsPreorder _â‰¤L_
  â‰¤L-isPreorder .refl {bottom} = tt
  â‰¤L-isPreorder .refl {< x >} = â‰¤-isPreorder .refl
  â‰¤L-isPreorder .trans {bottom} {bottom} {bottom} mâ‚ mâ‚‚ = tt
  â‰¤L-isPreorder .trans {bottom} {bottom} {< z >}  mâ‚ mâ‚‚ = tt
  â‰¤L-isPreorder .trans {bottom} {< y >}  {< z >}  mâ‚ mâ‚‚ = tt
  â‰¤L-isPreorder .trans {< x >}  {< y >}  {< z >}  mâ‚ mâ‚‚ = â‰¤-isPreorder .trans mâ‚ mâ‚‚

L : Preorder â†’ Preorder
L X .Preorder.Carrier = LCarrier (X .Preorder.Carrier)
L X .Preorder._â‰¤_ = _â‰¤L_ (X .Preorder.â‰¤-isPreorder)
L X .Preorder.â‰¤-isPreorder = â‰¤L-isPreorder (X .Preorder.â‰¤-isPreorder)

-- Binary products
module _ where
  open Preorder

  _Ã—_ : Preorder â†’ Preorder â†’ Preorder
  (X Ã— Y) .Carrier = Data.Product._Ã—_ (X .Carrier) (Y .Carrier)
  (X Ã— Y) ._â‰¤_ (xâ‚ , yâ‚) (xâ‚‚ , yâ‚‚) = (X ._â‰¤_ xâ‚ xâ‚‚) âˆ§ (Y ._â‰¤_ yâ‚ yâ‚‚)
  (X Ã— Y) .â‰¤-isPreorder .IsPreorder.refl = (X .â‰¤-refl) , (Y .â‰¤-refl)
  (X Ã— Y) .â‰¤-isPreorder .IsPreorder.trans (xâ‚â‰¤yâ‚ , xâ‚‚â‰¤yâ‚‚) (yâ‚â‰¤zâ‚ , yâ‚‚â‰¤zâ‚‚) =
    (X .â‰¤-trans xâ‚â‰¤yâ‚ yâ‚â‰¤zâ‚) , (Y .â‰¤-trans xâ‚‚â‰¤yâ‚‚ yâ‚‚â‰¤zâ‚‚)

  Ã—-â‰ƒ : âˆ€ {X Y : Preorder} {xâ‚ xâ‚‚ : X .Carrier} {yâ‚ yâ‚‚ : Y .Carrier} â†’
        _â‰ƒ_ X xâ‚ xâ‚‚ â†’ _â‰ƒ_ Y yâ‚ yâ‚‚ â†’ _â‰ƒ_ (X Ã— Y) (xâ‚ , yâ‚) (xâ‚‚ , yâ‚‚)
  Ã—-â‰ƒ (xâ‚â‰¤xâ‚‚ , xâ‚‚â‰¤xâ‚) (yâ‚â‰¤yâ‚‚ , yâ‚‚â‰¤yâ‚) .projâ‚ = xâ‚â‰¤xâ‚‚ , yâ‚â‰¤yâ‚‚
  Ã—-â‰ƒ (xâ‚â‰¤xâ‚‚ , xâ‚‚â‰¤xâ‚) (yâ‚â‰¤yâ‚‚ , yâ‚‚â‰¤yâ‚) .projâ‚‚ = xâ‚‚â‰¤xâ‚ , yâ‚‚â‰¤yâ‚

-- Arbitrary products
module _ (I : Set) (A : I â†’ Preorder) where
  open Preorder

  Î  : Preorder
  Î  .Carrier = âˆ€ i â†’ A i .Carrier
  Î  ._â‰¤_ xâ‚ xâ‚‚ = âˆ€ i â†’ A i ._â‰¤_ (xâ‚ i) (xâ‚‚ i)
  Î  .â‰¤-isPreorder .IsPreorder.refl i = A i .â‰¤-refl
  Î  .â‰¤-isPreorder .IsPreorder.trans xâ‰¤y yâ‰¤z i = A i .â‰¤-trans (xâ‰¤y i) (yâ‰¤z i)
